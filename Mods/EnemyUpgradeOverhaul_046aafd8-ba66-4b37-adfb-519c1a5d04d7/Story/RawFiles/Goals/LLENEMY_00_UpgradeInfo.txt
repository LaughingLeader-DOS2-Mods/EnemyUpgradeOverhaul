Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLENEMY_UpgradeInfo_InitSettings();
KBSECTION
PROC
LLENEMY_UpgradeInfo_Register((STRING)_Upgrade, (STRING)_DisplayName, (STRING)_Description)
THEN
DB_LLENEMY_UpgradeInfo_Info(_Upgrade, _DisplayName, _Description);

//REGION DEFAULT_ENTRIES
PROC
LLENEMY_UpgradeInfo_InitSettings()
THEN
LLENEMY_UpgradeInfo_Register("LLENEMY_TALENT_BACKSTAB", "<font color='#FFAB00'>Talent: Assassin</font>", "Character can backstab with non-dagger weapons.");
LLENEMY_UpgradeInfo_Register("LLENEMY_TALENT_LEECH", "<font color='#C80030'>Talent: Leech</font>", "Character is healed when standing in blood.");
LLENEMY_UpgradeInfo_Register("LLENEMY_TALENT_LONEWOLF", "<font color='#DC0015'>Talent: Lone Wolf</font>", "Character has +2 Max AP, +2 Recovery AP, +30% Vitality, +30% Physical Armour, +30% Magic Armour, and doubles invested points in attributes and combat abilities.");
LLENEMY_UpgradeInfo_Register("LLENEMY_TALENT_QUICKSTEP", "<font color='#FFAB00'>Talent: The Pawn</font>", "Character gets 1 AP worth of free movement each turn.");
LLENEMY_UpgradeInfo_Register("LLENEMY_TALENT_RANGERRANGE", "<font color='#FFAB00'>Talent: Quickdraw</font>", "Character has increased attack range of Bows and Crossbows by 2m.");
LLENEMY_UpgradeInfo_Register("LLENEMY_TALENT_WHATARUSH", "<font color='#47e982'>Talent: What a Rush</font>", "Character has increased recovery and maximum Action Points when their health is below 50%.");
LLENEMY_UpgradeInfo_Register("LLENEMY_TALENT_TORTURER", "<font color='#960000'>Talent: Torturer</font>", "Character has the ability for certain statuses to no longer be blocked by Magic or Physical Armour, and their duration is extended by one turn.");
LLENEMY_UpgradeInfo_Register("LLENEMY_TALENT_SADIST", "<font color='#ff5771'>Talent: Sadist</font>", "Melee attacks deal additional fire damage to burning targets, poison damage to poisoned targets and physical damage to bleeding targets.");
LLENEMY_UpgradeInfo_Register("LLENEMY_TALENT_HAYMAKER", "<font color='#b083ff'>Talent: Haymaker</font>", "Character's attacks never miss but they cannot deal critical strikes.");
LLENEMY_UpgradeInfo_Register("LLENEMY_TALENT_GLADIATOR", "<font color='#f59b00'>Talent: Gladiator</font>", "Every time character is hit with a melee attack while wielding a shield, they perform a counterattack.<br>Can happen only once per turn.");
LLENEMY_UpgradeInfo_Register("LLENEMY_TALENT_INDOMITABLE", "<font color='#e94947'>Talent: Indomitable</font>", "Character has immunity to Stunned, Frozen, Knocked Down, Polymorphed, Petrified, Crippled for 1 turn after being affected by one of these statuses.<br>Can happen once every 3 turns.");
LLENEMY_UpgradeInfo_Register("LLENEMY_TALENT_SOULCATCHER", "<font color='#73F6FF'>Talent: Soulcatcher</font>", "When an allied character dies, a Zombie Crawler is raised at their corpse, under their control. Zombie Crawler lasts 3 turns or until character is resurrected. Range 12m. Does not affect summoned creatures.");
LLENEMY_UpgradeInfo_Register("LLENEMY_TALENT_MASTERTHIEF", "<font color='#C9AA58'>Talent: Master Thief</font>", "Character has a chance to steal a consumable item on attack.<br>Can only happen once per turn.");
LLENEMY_UpgradeInfo_Register("LLENEMY_TALENT_GREEDYVESSEL", "<font color='#e9d047'>Talent: Greedy Vessel</font>", "Every time someone casts a Source spell in combat, character has a 20% chance of gaining a Source Point.");
LLENEMY_UpgradeInfo_Register("LLENEMY_TALENT_MAGICCYCLES", "<font color='#22c3ff'>Talent: Magic Cycles</font>", "At the start of an encounter, you gain one of two statuses: Cycle of Fire and Water or Cycle of Earth and Air. Cyle of Fire and Water increases Pyro and Hyro by 2. Cyle of Earth and Air increases Geo and Aero by 2. Statuses swap each turn.");
LLENEMY_UpgradeInfo_Register("LLENEMY_INF_NECROFIRE", "<font color='#7F00FF'>Infernoblazer</font>", "");
LLENEMY_UpgradeInfo_Register("LLENEMY_INF_NECROFIRE_G", "<font color='#FE6E27'>Elite Infernoblazer</font>", "");
LLENEMY_UpgradeInfo_Register("LLENEMY_INF_WATER", "<font color='#188EDE'>Cascader</font>", "");
LLENEMY_UpgradeInfo_Register("LLENEMY_INF_WATER_G", "<font color='#FE6E27'>Elite Cascader</font>", "");
LLENEMY_UpgradeInfo_Register("LLENEMY_INF_BLESSED_ICE", "<font color='#CFECFF'>Heatsapper</font>", "");
LLENEMY_UpgradeInfo_Register("LLENEMY_INF_BLESSED_ICE_G", "<font color='#FE6E27'>Elite Heatsapper</font>", "");
LLENEMY_UpgradeInfo_Register("LLENEMY_INF_POISON", "<font color='#65C900'>Venomstriker</font>", "");
LLENEMY_UpgradeInfo_Register("LLENEMY_INF_POISON_G", "<font color='#FE6E27'>Elite Venomstriker</font>", "");
LLENEMY_UpgradeInfo_Register("LLENEMY_INF_ACID", "<font color='#81AB00'>Melter</font>", "");
LLENEMY_UpgradeInfo_Register("LLENEMY_INF_ACID_G", "<font color='#FE6E27'>Elite Melter</font>", "");
LLENEMY_UpgradeInfo_Register("LLENEMY_INF_ELECTRIC", "<font color='#7D71D9'>Circuitbreaker</font>", "");
LLENEMY_UpgradeInfo_Register("LLENEMY_INF_ELECTRIC_G", "<font color='#FE6E27'>Elite Circuitbreaker</font>", "");
LLENEMY_UpgradeInfo_Register("LLENEMY_INF_CURSED_ELECTRIC", "<font color='#7F25D4'>Teslacoil</font>", "");
LLENEMY_UpgradeInfo_Register("LLENEMY_INF_CURSED_ELECTRIC_G", "<font color='#FE6E27'>Elite Teslacoil</font>", "");
LLENEMY_UpgradeInfo_Register("LLENEMY_INF_BLOOD", "<font color='#AA3938'>Bloodbender</font>", "");
LLENEMY_UpgradeInfo_Register("LLENEMY_INF_BLOOD_G", "<font color='#FE6E27'>Elite Bloodbender</font>", "");
LLENEMY_UpgradeInfo_Register("LLENEMY_INF_OIL", "<font color='#C7A758'>Earthcracker</font>", "");
LLENEMY_UpgradeInfo_Register("LLENEMY_INF_OIL_G", "<font color='#FE6E27'>Elite Earthcracker</font>", "");
LLENEMY_UpgradeInfo_Register("LLENEMY_INF_FIRE", "<font color='#FE6E27'>Firestarter</font>", "");
LLENEMY_UpgradeInfo_Register("LLENEMY_INF_FIRE_G", "<font color='#FE6E27'>Elite Firestarter</font>", "");
LLENEMY_UpgradeInfo_Register("LLENEMY_BONUS_TREASURE_ROLL", "<font color='#D040D0'>Bonus Treasure</font>", "Character rolled a <font color='#FF0000'>Critical Failure (0)</font>, and now has to give bonus treasure if killed before this status expires. Hurry!");
LLENEMY_UpgradeInfo_Register("LLENEMY_IMMUNITY_LOSECONTROL", "<font color='#FFAB00'>Perfect Control</font>", "Character is immune to losing control, either through Fear, Charm, Madness, or being turned into a chicken.");
LLENEMY_UpgradeInfo_Register("LLENEMY_DOUBLE_DIP", "<font color='#7F00FF'>Double Dip</font>", "Character is growing even more powerful through a second round of upgrades!");
LLENEMY_UpgradeInfo_Register("LLENEMY_PERSEVERANCE_MASTERY", "<font color='#E4CE93'>Perseverance Mastery</font>", "Character is persevering through sheer force of will.");
LLENEMY_UpgradeInfo_Register("LLENEMY_BONUSSKILLS_SINGLE", "<font color='#F1D466'>Bonus Skill</font>", "Character has a new bonus skill.");
LLENEMY_UpgradeInfo_Register("LLENEMY_BONUSSKILLS_SET_NORMAL", "<font color='#B823CB'>Bonus Skillset</font>", "Character has a set of new bonus skills.");
LLENEMY_UpgradeInfo_Register("LLENEMY_BONUSSKILLS_SOURCE", "<font color='#46B195'>Bonus Source Skill</font>", "Character has a new bonus source skill.");
LLENEMY_UpgradeInfo_Register("LLENEMY_BONUSSKILLS_SET_ELITE", "<font color='#73F6FF'>Elite Skillset</font>", "Character has an elite set of new bonus skills.");
//END_REGION

//REGION REGISTER_ID
/*
The character data in StatusGetDescriptionParam doesn't have a reference to the object's UUID, 
so we use Hearing for the ID.
 */

/* [OSITOOLS_ONLY]
PROC
LLENEMY_UpgradeInfo_Internal_Update((CHARACTERGUID)_Character, (STRING)_Upgrade)
AND
NOT DB_LLENEMY_UpgradeInfo_Temp_ID(_Character, _)
THEN
LeaderLog_Log("DEBUG", "[LLENEMY_00_UpgradesMain:Update] Creating unique ID.");
NRD_LuaCall("LLENEMY_EXT_CreateUpgradeInfoID", (STRING)_Character);
*/

PROC
LLENEMY_UpgradeInfo_Internal_StoreUpgradeID((CHARACTERGUID)_Character, (INTEGER)_Hearing)
AND
DB_LLENEMY_UpgradeInfo_Temp_ID(_Character, _Last)
THEN
NOT DB_LLENEMY_UpgradeInfo_Temp_ID(_Character, _Last);

PROC
LLENEMY_UpgradeInfo_Internal_StoreUpgradeID((CHARACTERGUID)_Character, (INTEGER)_Hearing)
THEN
DB_LLENEMY_UpgradeInfo_Temp_ID(_Character, _Hearing);
//END_REGION

//REGION RESETTING
/* Lua data isn't persistant, so it needs to be refreshed using databases. */
IF
TextEventSet("leaderlib_luareset")
THEN
TimerCancel("Timers_LLENEMY_ResetUpgradeInfo");
TimerLaunch("Timers_LLENEMY_ResetUpgradeInfo", 50);

IF
TimerFinished("Timers_LLENEMY_ResetUpgradeInfo")
AND
DB_LLENEMY_UpgradeInfo_Temp_ID(_Character, _HearingID)
THEN
LLENEMY_UpgradeInfo_Internal_Update(_Character, "");

/* [OSITOOLS_ONLY]
IF
GameStarted(_,_)
AND
DB_LLENEMY_UpgradeInfo_Temp_ID(_Character, _HearingID)
THEN
LLENEMY_UpgradeInfo_Internal_Update(_Character, "");
*/
//END_REGION

//REGION UPGRADE_INFO
PROC
LLENEMY_Upgrades_ApplyUpgrade((CHARACTERGUID)_Character, (STRING)_Group, (STRING)_Type, (STRING)_Upgrade, (REAL)_Duration, (INTEGER)_CP)
AND
LeaderLib_QRY_ExtenderIsActive()
THEN
LLENEMY_UpgradeInfo_Internal_Update(_Character, _Upgrade);
LeaderLib_Timers_StartObjectTimer(_Character, 250, "Timers_LLENEMY_UpgradeInfo_SendInfo", "LLENEMY_UpgradeInfo_SendInfo");

IF
StoryEvent((CHARACTERGUID)_Character, "LLENEMY_UpgradeInfo_SendInfo")
THEN
LLENEMY_UpgradeInfo_Internal_SendInfo(_Character);

IF
StoryEvent((CHARACTERGUID)_Character, "LLENEMY_UpgradeInfo_SendInfo")
AND
LLENEMY_UpgradeInfo_QRY_HasInfo(_Character)
AND
NOT LeaderLib_Helper_QRY_HasStatus(_Character, "LLENEMY_UPGRADE_INFO")
THEN
ApplyStatus(_Character, "LLENEMY_UPGRADE_INFO", -1.0, 0, _Character);

QRY
LLENEMY_UpgradeInfo_QRY_HasInfo((CHARACTERGUID)_Character)
AND
DB_LLENEMY_UpgradeInfo_Temp_ActiveUpgrades(_Character, _Upgrade)
THEN
DB_NOOP(1);

// [BEGIN_NO_OSITOOLS]
PROC
LLENEMY_UpgradeInfo_Internal_Update((CHARACTERGUID)_Character, (STRING)_Upgrade)
THEN
DB_NOOP(1);
// [END_NO_OSITOOLS]

PROC
LLENEMY_UpgradeInfo_Internal_Update((CHARACTERGUID)_Character, (STRING)_Upgrade)
AND
_Upgrade != ""
AND
DB_LLENEMY_UpgradeInfo_Info(_Upgrade, _DisplayName, _Description)
THEN
DB_LLENEMY_UpgradeInfo_Temp_ActiveUpgrades(_Character, _Upgrade);

PROC
LLENEMY_UpgradeInfo_Internal_Update((CHARACTERGUID)_Character, (STRING)_Upgrade)
AND
DB_LLENEMY_UpgradeInfo_Temp_InfoString(_Character, _Last)
THEN
NOT DB_LLENEMY_UpgradeInfo_Temp_InfoString(_Character, _Last);

PROC
LLENEMY_UpgradeInfo_Internal_SendInfo((CHARACTERGUID)_Character)
THEN
DB_LLENEMY_UpgradeInfo_Temp_InfoString(_Character, "");

PROC
LLENEMY_UpgradeInfo_Internal_SendInfo((CHARACTERGUID)_Character)
AND
DB_LLENEMY_UpgradeInfo_Temp_ActiveUpgrades(_Character, _Upgrade)
AND
DB_LLENEMY_UpgradeInfo_Info(_Upgrade, _DisplayName, _Description)
AND
DB_LLENEMY_UpgradeInfo_Temp_InfoString(_Character, _Current)
AND
StringConcatenate(_Current, ";", _a)
AND
StringConcatenate(_a, _DisplayName, _Next)
THEN
NOT DB_LLENEMY_UpgradeInfo_Temp_InfoString(_Character, _Current);
DB_LLENEMY_UpgradeInfo_Temp_InfoString(_Character, _Next);

/* [OSITOOLS_ONLY]
PROC
LLENEMY_UpgradeInfo_Internal_SendInfo((CHARACTERGUID)_Character)
AND
GetUUID(_Character, _UUID)
AND
DB_LLENEMY_UpgradeInfo_Temp_InfoString(_Character, _Info)
AND
_Info != ""
THEN
NRD_LuaCall("LLENEMY_EXT_StoreUpgradeInfo", _UUID, _Info);
*/
//END_REGION

//REGION CLEARING
PROC
LLENEMY_UpgradeInfo_Internal_ClearDataForObject((CHARACTERGUID)_Character)
AND
DB_LLENEMY_UpgradeInfo_Temp_ActiveUpgrades(_Character, _Upgrade)
THEN
NOT DB_LLENEMY_UpgradeInfo_Temp_ActiveUpgrades(_Character, _Upgrade);

PROC
LLENEMY_UpgradeInfo_Internal_ClearDataForObject((CHARACTERGUID)_Character)
AND
DB_LLENEMY_UpgradeInfo_Temp_ID(_Character, _Hearing)
THEN
NOT DB_LLENEMY_UpgradeInfo_Temp_ID(_Character, _Hearing);

PROC
LLENEMY_UpgradeInfo_Internal_ClearDataForObject((CHARACTERGUID)_Character)
AND
DB_LLENEMY_UpgradeInfo_Temp_InfoString(_Character, _Info)
THEN
NOT DB_LLENEMY_UpgradeInfo_Temp_InfoString(_Character, _Info);

IF
CharacterDied(_Character)
AND
LLENEMY_UpgradeInfo_QRY_HasInfo(_Character)
THEN
LLENEMY_UpgradeInfo_Internal_ClearDataForObject(_Character);

IF
RegionEnded(_)
AND
DB_LLENEMY_UpgradeInfo_Temp_ID(_Character, _Hearing)
THEN
LLENEMY_UpgradeInfo_Internal_ClearDataForObject(_Character);

/* [OSITOOLS_ONLY]
PROC
LLENEMY_UpgradeInfo_Internal_ClearDataForObject((CHARACTERGUID)_Character)
AND
GetUUID(_Character, _UUID)
THEN
NRD_LuaCall("LLENEMY_EXT_RemoveUpgradeInfo", _UUID);
*/
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_EnemyUpgradeOverhaul"