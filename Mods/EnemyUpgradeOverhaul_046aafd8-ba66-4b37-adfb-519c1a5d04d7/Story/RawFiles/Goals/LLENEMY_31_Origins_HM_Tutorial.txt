Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLENEMY_HardMode_InitLevel("TUT_Tutorial_A");
KBSECTION
//REGION TOPDECK_INIT
PROC
LLENEMY_HardMode_InitLevel("TUT_Tutorial_A")
THEN
LLENEMY_HardMode_SetupNPC(S_TUT_TopDeckMagister1_de400bda-b14e-4cff-b5f5-737781437902, "TUT_Tutorial_A", 1);
LLENEMY_HardMode_SetupNPC(S_TUT_TopDeckMagister2_e2d47d73-4f9d-4de2-8a3c-c774a0ea114a, "TUT_Tutorial_A", 1);
//LLENEMY_HardMode_SetupNPC(S_TUT_TopDeck_LivingMagister1_b5e74192-498f-4eb3-844a-4a817f9802d3, "TUT_Tutorial_A", 0);
// This dood starts at 50% health
CharacterSetHitpointsPercentage(TUT_CargoDeck_Humans_Male_Magister_Grunt_000_a06e61dd-58c6-4119-99be-716c3a4fc1ef, 100.0);
LeaderLib_Helper_AddAndEquipItemTemplate(TUT_CargoDeck_Humans_Male_Magister_Grunt_000_a06e61dd-58c6-4119-99be-716c3a4fc1ef, "47131328-5335-4349-999b-c0fa4ad0a806", 0);
LeaderLib_Helper_AddAndEquipItemTemplate(TUT_CargoDeck_Humans_Male_Magister_Grunt_000_a06e61dd-58c6-4119-99be-716c3a4fc1ef, "ec4769e6-bcfc-44e0-9e9c-de62e0bdf407", 0);
//END_REGION

//REGION LEVEL_UP_TENTACLES
IF
ObjectEnteredCombat(S_TUT_TopDeckVoidling10_bd0123ae-26fd-4dad-8326-b6ae9a3fc1c5, _CombatID)
AND
GlobalGetFlag("LLENEMY_EnemyLevelingEnabled", 1)
AND
CharacterGetHostCharacter(_Host)
AND
CharacterGetLevel(_Host, _HostLevel)
AND
DB_LLENEMY_LevelModifier(_Modifier)
AND
IntegerSum(_HostLevel, _Modifier, _TargetLevel)
THEN
//LLENEMY_Scaling_Internal_LevelUpCharacterWithCap(S_TUT_MiddleDeckTentacle_1_80c1f2cb-1596-4dec-a127-bec0e5ced33a, _TargetLevel, 0);
//LLENEMY_Scaling_Internal_LevelUpCharacterWithCap(S_TUT_MiddleDeckTentacle_2_23461d8e-fcab-415d-8be4-0def913dfbb4, _TargetLevel, 0);
//LLENEMY_Scaling_Internal_LevelUpCharacterWithCap(S_TUT_MiddleDeckTentacle_3_82776900-ea28-435c-8e32-fb9135ca38b7, _TargetLevel, 0);
LLENEMY_Scaling_Internal_LevelUpCharacterWithCap(S_TUT_TopDeck_Tentacle1_6cbd4fbf-a9f0-403c-8ee1-5679e4ae03a6, _TargetLevel, 0);
LLENEMY_Scaling_Internal_LevelUpCharacterWithCap(S_TUT_TopDeck_Tentacle4_488645e1-3d74-4b3e-83fb-ab2dde52387b, _TargetLevel, 0);
//END_REGION

//REGION KEEP_BOSS_ALIVE
IF
AutomatedDialogStarted("TUT_AD_TopDeckMagister2", _Instance)
THEN
ProcObjectTimerCancel(CHARACTERGUID_S_TUT_TopDeckMagister1_de400bda-b14e-4cff-b5f5-737781437902, "Timers_LLENEMY_Elites_TUT_TopDeck_MakeMortal");
ProcObjectTimer(CHARACTERGUID_S_TUT_TopDeckMagister1_de400bda-b14e-4cff-b5f5-737781437902, "Timers_LLENEMY_Elites_TUT_TopDeck_CastDome", 250);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char, "Timers_LLENEMY_Elites_TUT_TopDeck_CastDome")
THEN
LeaderLib_Helper_ClearActionQueue(_Char, 1);
CharacterUseSkill(_Char, "Dome_LLENEMY_EnemyCircleOfProtection", CHARACTERGUID_S_TUT_TopDeckMagister2_e2d47d73-4f9d-4de2-8a3c-c774a0ea114a, 1, 1, 1);

IF
SkillCast(CHARACTERGUID_S_TUT_TopDeckMagister1_de400bda-b14e-4cff-b5f5-737781437902, "Dome_LLENEMY_EnemyCircleOfProtection", _, _)
THEN
ProcObjectTimer(CHARACTERGUID_S_TUT_TopDeckMagister1_de400bda-b14e-4cff-b5f5-737781437902, "Timers_LLENEMY_Elites_TUT_TopDeck_MakeMortal", 1500);

IF
//SkillCast(S_TUT_TopDeck_Tentacle4_488645e1-3d74-4b3e-83fb-ab2dde52387b, "Zone_Quest_EnemyTentacleSweep", _, _)
OnStageChanged(ITEMGUID_S_TUT_FireSurfaceHelper_77d9fcb1-c857-4db9-bd31-19b5cfa4af5a, 0)
THEN
SetVarInteger(CHARACTERGUID_S_TUT_TopDeckMagister1_de400bda-b14e-4cff-b5f5-737781437902, "FleeFromDangerousSurface", 0);
SetVarInteger(CHARACTERGUID_S_TUT_TopDeckMagister2_e2d47d73-4f9d-4de2-8a3c-c774a0ea114a, "FleeFromDangerousSurface", 0);
ProcObjectTimer(CHARACTERGUID_S_TUT_TopDeckMagister1_de400bda-b14e-4cff-b5f5-737781437902, "Timers_LLENEMY_Elites_TUT_TopDeck_MakeMortal", 1000);
ProcObjectTimer(CHARACTERGUID_S_TUT_TopDeckMagister2_e2d47d73-4f9d-4de2-8a3c-c774a0ea114a, "Timers_LLENEMY_Elites_TUT_TopDeck_MakeMortal", 1000);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char, "Timers_LLENEMY_Elites_TUT_TopDeck_MakeMortal")
THEN
ProcSetInvulnerable(_Char, 0);
CharacterSetImmortal(_Char, 0);
//SetFaction(_Char, "Evil NPC");
LeaderLib_Helper_MakeHostileToPlayers(_Char);
ApplyStatus(_Char, "ETHEREAL_SOLES", 48.0, 1, _Char);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char, "Timers_LLENEMY_Elites_TUT_TopDeck_MakeMortal")
AND
CharacterIsInCombat(_Char, 0)
AND
GetRandomPositionInTrigger(TRIGGERGUID_S_TUT_StrikingTentacle_Trigger1_61d8d246-d5c0-4a26-b9fa-a12e3807ae40, _x, _y, _z)
THEN
ProcCharacterMoveToPosition(_Char, _x, _y, _z, 1, "LLENEMY_TopDeckMagister_Arrived");

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char, "Timers_LLENEMY_Elites_TUT_TopDeck_MakeMortal")
AND
CharacterIsInCombat(_Char, 0)
AND
NOT DB_StoryMoving(_Char,1)
THEN
ProcCharacterMoveTo(_Char, TRIGGERGUID_S_TUT_StrikingTentacle_Trigger1_61d8d246-d5c0-4a26-b9fa-a12e3807ae40, 0, "LLENEMY_TopDeckMagister_Arrived");

IF
StoryEvent((CHARACTERGUID)_Char, "LLENEMY_TopDeckMagister_Arrived")
AND
GetStatusTurns(_Char, "ETHEREAL_SOLES", _Turns)
AND
_Turns > 0
THEN
RemoveStatus(_Char, "ETHEREAL_SOLES");
//END_REGION

//REGION RESURRECT_TOPDECK_GRUNT
IF
CharacterKilledBy(S_TUT_TopDeck_LivingMagister1_b5e74192-498f-4eb3-844a-4a817f9802d3, S_TUT_TopDeck_Tentacle1_6cbd4fbf-a9f0-403c-8ee1-5679e4ae03a6, S_TUT_TopDeck_Tentacle1_6cbd4fbf-a9f0-403c-8ee1-5679e4ae03a6)
AND
ObjectGetFlag(S_TUT_TopDeck_LivingMagister1_b5e74192-498f-4eb3-844a-4a817f9802d3, "LLENEMY_TopMagister_Resurrected", 0)
AND
GetTemplate(S_TUT_TopDeck_LivingMagister1_b5e74192-498f-4eb3-844a-4a817f9802d3, _MagisterTemplate)
AND
CharacterCreateOutOfSightToObject(_MagisterTemplate, S_TUT_TopDeck_LivingMagister1_b5e74192-498f-4eb3-844a-4a817f9802d3, S_TUT_TopDeck_Tentacle1_6cbd4fbf-a9f0-403c-8ee1-5679e4ae03a6, 0, "", _Replacement)
THEN
ObjectSetFlag(S_TUT_TopDeck_LivingMagister1_b5e74192-498f-4eb3-844a-4a817f9802d3, "LLENEMY_TopMagister_Resurrected", 0);
//SetOnStage(_Replacement, 0);
ApplyStatus(_Replacement, "LEADERLIB_NO_VISUAL", -1.0, 1, _Replacement);
CharacterTransformFromCharacter(_Replacement, S_TUT_TopDeck_LivingMagister1_b5e74192-498f-4eb3-844a-4a817f9802d3, 0, 1, 1, 1, 1, 0, 0);
SetStoryEvent(_Replacement, "GEN_FallAndLie");
ObjectSetFlag(_Replacement, "LLENEMY_TopMagister_ResurrectedClone", 0);
DB_LLENEMY_TUT_Temp_ReplacementMagister(S_TUT_TopDeck_LivingMagister1_b5e74192-498f-4eb3-844a-4a817f9802d3, _Replacement);
//ProcObjectTimer(S_TUT_TopDeck_LivingMagister1_b5e74192-498f-4eb3-844a-4a817f9802d3, "Timers_LLENEMY_Elites_TUT_TopDeckLiving_SetupDying", 500);
ProcObjectTimerFinished(S_TUT_TopDeck_LivingMagister1_b5e74192-498f-4eb3-844a-4a817f9802d3, "Timers_LLENEMY_Elites_TUT_TopDeckLiving_SetupDying");
ProcObjectTimer(_Replacement, "Timers_LLENEMY_Elites_TUT_TopDeckLiving_SetupCombatTracker", 500);
LLENEMY_HardMode_SetupNPC(_Replacement, "TUT_Tutorial_A", 0);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char, "Timers_LLENEMY_Elites_TUT_TopDeckLiving_SetupDying")
AND
DB_LLENEMY_TUT_Temp_ReplacementMagister(_Char, _Replacement)
AND
GetPosition(_Char, _x, _y, _z)
THEN
TeleportToPosition(_Replacement, _x, _y, _z, "", 0, 1);
SetTag(_Replacement, "AI_IGNORED_TARGET");
PlayAnimation(_Replacement, "knockdown_loop");
CharacterSetHitpointsPercentage(_Replacement, 1.0);
CharacterSetImmortal(_Replacement, 1);
SetCanJoinCombat(_Replacement, 0);
SetVarInteger(_Replacement, "bool_CanCower", 0);
SetVarInteger(_Replacement, "bool_CanFleeWhileCowering", 0);
SetVarInteger(_Replacement, "FleeFromDangerousSurface", 0);
SetOnStage(_Char, 0);
MoveAllItemsTo(_Char, _Replacement, 0, 0, 1);
//SetOnStage(_Replacement, 1);
RemoveStatus(_Replacement, "LEADERLIB_NO_VISUAL");
ApplyStatus(_Replacement, "LLENEMY_FAKE_DYING", -1.0, 1, _Replacement);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Replacement, "Timers_LLENEMY_Elites_TUT_TopDeckLiving_SetupCombatTracker")
AND
DB_CombatCharacters(S_TUT_TopDeckVoidling10_bd0123ae-26fd-4dad-8326-b6ae9a3fc1c5, _CombatID)
THEN
LeaderLib_Turns_TrackCombat(_CombatID, "LLENEMY_TopDeckLivingMagister_GetUp", 5);

PROC
LeaderLib_Turns_OnTurnCounterComplete_Combat((STRING)_ID, (INTEGER)_CombatID, "LLENEMY_TopDeckLivingMagister_GetUp")
AND
DB_LLENEMY_TUT_Temp_ReplacementMagister(_Char, _Replacement)
THEN
NOT DB_LLENEMY_TUT_Temp_ReplacementMagister(_Char, _Replacement);
RemoveStatus(_Replacement, "LLENEMY_FAKE_DYING");
CharacterSetAnimationOverride(_Replacement, "");
PlayAnimation(_Replacement, "knockdown_getup", "LLENEMY_TopLivingMagister_StoodUp");
CharacterSetHitpointsPercentage(_Replacement, 66.0);

IF
StoryEvent((CHARACTERGUID)_Replacement, "LLENEMY_TopLivingMagister_StoodUp")
THEN
CharacterSetImmortal(_Replacement, 0);
SetCanJoinCombat(_Replacement, 1);
//SetFaction(_Replacement, "Evil NPC");
ProcSetInvulnerable(_Replacement, 0);
CharacterSetImmortal(_Replacement, 0);
LeaderLib_Helper_MakeHostileToPlayers(_Replacement);
LLENEMY_Rewards_GenerateTreasure(_Replacement, "ST_RingAmuletBeltRare", 1);

/* [OSITOOLS_ONLY]
IF
StoryEvent((CHARACTERGUID)_Replacement, "LLENEMY_TopLivingMagister_StoodUp")
AND
GetUUID(_Replacement, _UUID)
THEN
NRD_LuaCall("LeaderLib_Ext_SetCustomNameWithLocalization", _UUID, "h8350321agf54cg4528gb29egcb7dbb16cdc0", "Magister Knight (Not Dead)");
*/

//END_REGION

//REGION TREASURE
IF
CharacterDied(S_TUT_TopDeckMagister2_e2d47d73-4f9d-4de2-8a3c-c774a0ea114a)
/* [OSITOOLS_ONLY]
AND
NRD_CharacterGetInt(S_TUT_TopDeckMagister2_e2d47d73-4f9d-4de2-8a3c-c774a0ea114a, "Resurrected", 0) // Never resurrected
*/
THEN
LLENEMY_Rewards_GenerateTreasure(S_TUT_TopDeckMagister2_e2d47d73-4f9d-4de2-8a3c-c774a0ea114a, "RewardBig", 1);
LLENEMY_Rewards_GenerateTreasure(S_TUT_TopDeckMagister2_e2d47d73-4f9d-4de2-8a3c-c774a0ea114a, "ST_SkillbookPolymorph", 1);

IF
CharacterDied(S_TUT_TopDeckMagister1_de400bda-b14e-4cff-b5f5-737781437902)
/* [OSITOOLS_ONLY]
AND
NRD_CharacterGetInt(S_TUT_TopDeckMagister1_de400bda-b14e-4cff-b5f5-737781437902, "Resurrected", 0) // Never resurrected
*/
THEN
LLENEMY_Rewards_GenerateTreasure(S_TUT_TopDeckMagister1_de400bda-b14e-4cff-b5f5-737781437902, "ST_SkillbookWarrior", 1);
LLENEMY_Rewards_GenerateTreasure(S_TUT_TopDeckMagister1_de400bda-b14e-4cff-b5f5-737781437902, "ST_SkillbookStarter", 1);
//END_REGION

//REGION BLOCK_HATCH
PROC
ProcBlockUseOfItem((CHARACTERGUID)_Player, BLD_Ship_Exterior_Hatch_A_000_14ed9516-7dc8-4605-bb61-67ac109eca25)
AND
CharacterIsPlayer(_Player, 1)
THEN
DB_CustomUseItemResponse(_Player, BLD_Ship_Exterior_Hatch_A_000_14ed9516-7dc8-4605-bb61-67ac109eca25, 0);
Proc_StartDialog(1, "CMB_AD_PC_Locked", _Player);
//END_REGION

EXITSECTION
SysClear("DB_LLENEMY_TUT_Temp_ReplacementMagister", 2);
ENDEXITSECTION
ParentTargetEdge "LLENEMY_30_Origins_HardMode"