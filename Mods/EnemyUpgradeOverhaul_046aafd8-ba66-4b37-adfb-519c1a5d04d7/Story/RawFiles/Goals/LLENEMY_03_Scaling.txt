Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLENEMY_Scaling_InitSettings();
KBSECTION
//REGION SETTINGS
PROC
LLENEMY_Scaling_InitSettings()
THEN
DB_LLENEMY_LevelModifier(0);
//END_REGION

//REGION MODIFIER_ADJUSTING
IF
ObjectFlagSet("LLENEMY_EnemyLeveling_Increase1", _Speaker, _)
THEN
ObjectClearFlag(_Speaker, "LLENEMY_EnemyLeveling_Increase1");
LLENEMY_Scaling_AdjustLevelModifier(1);

IF
ObjectFlagSet("LLENEMY_EnemyLeveling_Decrease1", _Speaker, _)
THEN
ObjectClearFlag(_Speaker, "LLENEMY_EnemyLeveling_Decrease1");
LLENEMY_Scaling_AdjustLevelModifier(-1);

IF
ObjectFlagSet("LLENEMY_EnemyLeveling_Reset", _Speaker, _)
THEN
ObjectClearFlag(_Speaker, "LLENEMY_EnemyLeveling_Reset");
LLENEMY_Scaling_ResetLevelModifier();

/*
IF
DialogEnded("LLENEMY_Settings", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, _Speaker)
THEN
ObjectClearFlag(_Speaker, "LLENEMY_EnemyLeveling_Reset");
ObjectClearFlag(_Speaker, "LLENEMY_EnemyLeveling_Increase1");
ObjectClearFlag(_Speaker, "LLENEMY_EnemyLeveling_Decrease1");
*/

PROC
LLENEMY_Scaling_AdjustLevelModifier((INTEGER)_ByAmount)
AND
DB_LLENEMY_LevelModifier(_Modifier)
AND
IntegerSum(_Modifier, _ByAmount, _NextModifier)
AND
IntegerMax(0, _NextModifier, _ClampedModifier)
THEN
NOT DB_LLENEMY_LevelModifier(_Modifier);
DB_LLENEMY_LevelModifier(_ClampedModifier);
DialogSetVariableInt("LLENEMY_Settings", "LLENEMY_EnemyLevelingModifier_374d0f1d-bd61-4736-ad24-b1876a803c2f", _ClampedModifier);

PROC
LLENEMY_Scaling_ResetLevelModifier()
AND
DB_LLENEMY_LevelModifier(_Modifier)
THEN
NOT DB_LLENEMY_LevelModifier(_Modifier);
DB_LLENEMY_LevelModifier(0);
DialogSetVariableInt("LLENEMY_Settings", "LLENEMY_EnemyLevelingModifier_374d0f1d-bd61-4736-ad24-b1876a803c2f", 0);
//END_REGION

//REGION LEVELING
PROC
LLENEMY_Scaling_LevelUpEnemy((CHARACTERGUID)_Character)
AND
DB_GlobalFlag("LLENEMY_EnemyLevelingEnabled")
AND
CharacterGetHostCharacter(_Host)
AND
CharacterGetLevel(_Host, _Level)
AND
DB_LLENEMY_LevelModifier(_Modifier)
AND
IntegerSum(_Level, _Modifier, _TargetLevel)
THEN
CharacterLevelUpTo(_Character, _TargetLevel);
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_EnemyUpgradeOverhaul"