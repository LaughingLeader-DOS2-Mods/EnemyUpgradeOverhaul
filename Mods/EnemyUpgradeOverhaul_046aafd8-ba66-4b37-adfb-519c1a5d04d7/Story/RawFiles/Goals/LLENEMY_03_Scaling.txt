Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLENEMY_Scaling_InitSettings();
KBSECTION
//REGION SETTINGS
PROC
LLENEMY_Scaling_InitSettings()
THEN
DB_LLENEMY_LevelModifier(0);
//END_REGION

//REGION MODIFIER_ADJUSTING
IF
ObjectFlagSet("LLENEMY_EnemyLeveling_Increase1", _Speaker, _)
THEN
ObjectClearFlag(_Speaker, "LLENEMY_EnemyLeveling_Increase1");
LLENEMY_Scaling_AdjustLevelModifier(1);

IF
ObjectFlagSet("LLENEMY_EnemyLeveling_Decrease1", _Speaker, _)
THEN
ObjectClearFlag(_Speaker, "LLENEMY_EnemyLeveling_Decrease1");
LLENEMY_Scaling_AdjustLevelModifier(-1);

IF
ObjectFlagSet("LLENEMY_EnemyLeveling_Reset", _Speaker, _)
THEN
ObjectClearFlag(_Speaker, "LLENEMY_EnemyLeveling_Reset");
LLENEMY_Scaling_ResetLevelModifier();

IF
DialogStarted("LLENEMY_Settings", _)
THEN
LLENEMY_Scaling_UpdateDialogVariables();

IF
DB_GlobalFlag("LLENEMY_EnemyLevelingEnabled")
THEN
LLENEMY_Scaling_UpdateDialogVariables();

IF
GlobalFlagCleared("LLENEMY_EnemyLevelingEnabled")
THEN
LLENEMY_Scaling_UpdateDialogVariables();

PROC
LLENEMY_Scaling_UpdateDialogVariables()
AND
DB_LLENEMY_LevelModifier(_Modifier)
THEN
DialogSetVariableInt("LLENEMY_Settings", "LLENEMY_EnemyLeveling_Modifier_374d0f1d-bd61-4736-ad24-b1876a803c2f", _Modifier);

PROC
LLENEMY_Scaling_UpdateDialogVariables()
AND
NOT DB_GlobalFlag("LLENEMY_EnemyLevelingEnabled")
THEN
DialogSetVariableString("LLENEMY_Settings", "LLENEMY_EnemyLeveling_ExpectedLevel_4f2c6435-4ddc-4e2c-90d9-6506acb179ba", "Disabled");

PROC
LLENEMY_Scaling_UpdateDialogVariables()
AND
DB_GlobalFlag("LLENEMY_EnemyLevelingEnabled")
AND
CharacterGetHostCharacter(_Host)
AND
CharacterGetLevel(_Host, _Level)
AND
DB_LLENEMY_LevelModifier(_Modifier)
AND
IntegerSum(_Level, _Modifier, _EnemyLevel)
AND
IntegertoString(_EnemyLevel, _EnemyLevelStr)
THEN
DialogSetVariableString("LLENEMY_Settings", "LLENEMY_EnemyLeveling_ExpectedLevel_4f2c6435-4ddc-4e2c-90d9-6506acb179ba", _EnemyLevelStr);

/*
IF
DialogEnded("LLENEMY_Settings", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, _Speaker)
THEN
ObjectClearFlag(_Speaker, "LLENEMY_EnemyLeveling_Reset");
ObjectClearFlag(_Speaker, "LLENEMY_EnemyLeveling_Increase1");
ObjectClearFlag(_Speaker, "LLENEMY_EnemyLeveling_Decrease1");
*/

PROC
LLENEMY_Scaling_AdjustLevelModifier((INTEGER)_ByAmount)
AND
DB_LLENEMY_LevelModifier(_Modifier)
AND
IntegerSum(_Modifier, _ByAmount, _NextModifier)
AND
IntegerMax(-1, _NextModifier, _ClampedModifier1)
AND
IntegerMin(_ClampedModifier1, 20, _ClampedModifier2)
THEN
NOT DB_LLENEMY_LevelModifier(_Modifier);
DB_LLENEMY_LevelModifier(_ClampedModifier2);
LLENEMY_Scaling_UpdateDialogVariables();

PROC
LLENEMY_Scaling_ResetLevelModifier()
AND
DB_LLENEMY_LevelModifier(_Modifier)
THEN
NOT DB_LLENEMY_LevelModifier(_Modifier);
DB_LLENEMY_LevelModifier(0);
LLENEMY_Scaling_UpdateDialogVariables();
//END_REGION

//REGION LEVELING
PROC
LLENEMY_Scaling_LevelUpEnemy((CHARACTERGUID)_Character)
AND
DB_GlobalFlag("LLENEMY_EnemyLevelingEnabled")
AND
NOT DB_Origins(_Character) // Don't do anything for Origin characters, since they may be recruited again
AND
CharacterGetHostCharacter(_Host)
AND
CharacterGetLevel(_Host, _Level)
AND
DB_LLENEMY_LevelModifier(_Modifier)
AND
IntegerSum(_Level, _Modifier, _TargetLevel)
THEN
LLENEMY_Scaling_Internal_LevelUpEnemyWithCap(_Character, _TargetLevel);

PROC
LLENEMY_Scaling_Internal_LevelUpEnemyWithCap((CHARACTERGUID)_Character, (INTEGER)_TargetLevel)
AND
NOT DB_GlobalFlag("LLENEMY_Debug_LevelCapDisabled")
AND
IntegerMin(_TargetLevel, 35, _CappedLevel)
AND
_CappedLevel > 1
THEN
CharacterLevelUpTo(_Character, _CappedLevel);

//May crash if the target level is greater than 35
PROC
LLENEMY_Scaling_Internal_LevelUpEnemyWithCap((CHARACTERGUID)_Character, (INTEGER)_TargetLevel)
AND
DB_GlobalFlag("LLENEMY_Debug_LevelCapDisabled")
AND
_TargetLevel > 1
THEN
CharacterLevelUpTo(_Character, _TargetLevel);
//END_REGION

//REGION MASS_LEVELING
PROC
LLENEMY_Scaling_LevelAllInCombat()
AND
DB_CombatCharacters(_Character, _CombatID)
AND
LLENEMY_QRY_IsEnemyOfParty(_Character, _CombatID)
THEN
LLENEMY_Scaling_LevelUpEnemy(_Character);
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_EnemyUpgradeOverhaul"