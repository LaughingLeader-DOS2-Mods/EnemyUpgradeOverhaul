Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLENEMY_Singleplayer_CheckUserCount();
KBSECTION

//REGION USER_COUNT
IF
SavegameLoading(_,_,_,_)
THEN
SysClear("DB_LLENEMY_Users", 3);

IF
UserConnected(_UserID, _UserName, _UserProfileID) 
THEN
DB_LLENEMY_Users(_UserID, _UserName, _UserProfileID);
TimerCancel("Timers_LLENEMY_Singleplayer_CheckUserCount");
TimerLaunch("Timers_LLENEMY_Singleplayer_CheckUserCount", 500);

IF
UserDisconnected(_UserID, _, _)
AND
DB_LLENEMY_Users(_UserID, _UserName, _UserProfileID)
THEN
NOT DB_LLENEMY_Users(_UserID, _UserName, _UserProfileID);
TimerCancel("Timers_LLENEMY_Singleplayer_CheckUserCount");
TimerLaunch("Timers_LLENEMY_Singleplayer_CheckUserCount", 500);

IF
TimerFinished("Timers_LLENEMY_Singleplayer_ShowWarning")
THEN
LLENEMY_Singleplayer_CheckUserCount();

PROC
LLENEMY_Singleplayer_CheckUserCount()
AND
SysCount("DB_LLENEMY_Users", 3, _Count)
AND
_Count > 1
THEN
TimerCancel("Timers_LLENEMY_Singleplayer_ShowWarning");
TimerLaunch("Timers_LLENEMY_Singleplayer_ShowWarning", 1500);

PROC
LLENEMY_Singleplayer_CheckUserCount()
AND
SysCount("DB_LLENEMY_Users", 3, 1)
THEN
TimerCancel("Timers_LLENEMY_Singleplayer_ShowWarning");

IF
TimerFinished("Timers_LLENEMY_Singleplayer_ShowWarning")
AND
CharacterGetHostCharacter(_Host)
THEN
OpenMessageBox(_Host, "LLENEMY_MessageBox_SingleplayerUserWarning");
//END_REGION

//REGION UPGRADE_INFO
PROC
LLENEMY_Upgrades_ApplyUpgrade((CHARACTERGUID)_Character, (STRING)_Group, (STRING)_Type, (STRING)_Upgrade, (REAL)_Duration, (INTEGER)_CP)
AND
LeaderLib_QRY_ExtenderIsActive()
THEN
LLENEMY_UpgradeInfo_Internal_Update(_Character, _Upgrade);
//LeaderLib_Timers_StartObjectTimer(_Character, 50, "Timers_LLENEMY_UpgradeInfo_SortUpgrades", "LLENEMY_UpgradeInfo_SortUpgrades");
LeaderLib_Timers_StartObjectTimer(_Character, 250, "Timers_LLENEMY_UpgradeInfo_SendInfo", "LLENEMY_UpgradeInfo_SendInfo");

/* [OSITOOLS_ONLY]
IF
GameStarted(_,_)
AND
DB_LLENEMY_UpgradeInfo_Temp_ID(_Character, _HearingID)
THEN
LLENEMY_UpgradeInfo_Internal_Update(_Character, "");

PROC
LLENEMY_UpgradeInfo_Internal_Update((CHARACTERGUID)_Character, (STRING)_Upgrade)
AND
NOT DB_LLENEMY_UpgradeInfo_Temp_ID(_Character, _)
THEN
LeaderLog_Log("DEBUG", "[LLENEMY_00_UpgradesMain:Update] Creating unique ID.");
NRD_LuaCall("LLENEMY_EXT_CreateUpgradeInfoID", (STRING)_Character);
*/
//END_REGION

EXITSECTION
SysClear("DB_LLENEMY_Users", 3);
ENDEXITSECTION
ParentTargetEdge "LLENEMY_80_ToggledScripts"