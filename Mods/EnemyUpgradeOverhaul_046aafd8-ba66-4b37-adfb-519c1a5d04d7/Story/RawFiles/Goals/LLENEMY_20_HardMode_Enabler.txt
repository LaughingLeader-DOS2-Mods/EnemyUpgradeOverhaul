Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLENEMY_HardMode_CheckForCampaign();
KBSECTION

//REGION CALLBACKS
PROC
LLENEMY_HardMode_Enabled()
THEN
LLENEMY_Upgrades_ResetUpgradeData();

PROC
LLENEMY_HardMode_Disabled()
THEN
LLENEMY_Upgrades_ResetUpgradeData();
//END_REGION

//REGION GLOBAL_FLAG_EVENT
IF
GlobalFlagSet("LLENEMY_HardModeEnabled")
THEN
TimerCancel("Timers_LLENEMY_HardModeToggled");
TimerLaunch("Timers_LLENEMY_HardModeToggled", 3000);

IF
GlobalFlagCleared("LLENEMY_HardModeEnabled")
THEN
TimerCancel("Timers_LLENEMY_HardModeToggled");
TimerLaunch("Timers_LLENEMY_HardModeToggled", 3000);

IF
TimerFinished("Timers_LLENEMY_HardModeToggled")
AND
GlobalGetFlag("LLENEMY_HardModeEnabled", 1)
THEN
LLENEMY_HardMode_Enabled();

IF
TimerFinished("Timers_LLENEMY_HardModeToggled")
AND
GlobalGetFlag("LLENEMY_HardModeEnabled", 0)
THEN
LLENEMY_HardMode_Disabled();
//END_REGION

//REGION ORIGINS_ENABLING
PROC
LLENEMY_HardMode_CheckForCampaign()
AND
GlobalGetFlag("LLENEMY_HardModeEnabled", 1)
AND
SysStatus("DOS2ModWrapper", _Status)
AND
_Status > 1
THEN
SysActivateGoal("LLENEMY_30_Origins_HardMode");

PROC
Proc_GameModeStarted("Campaign", (INTEGER)_IsEditorMode)
AND
GlobalGetFlag("LLENEMY_HardModeEnabled", 1)
THEN
SysActivateGoal("LLENEMY_30_Origins_HardMode");

PROC
LLENEMY_HardMode_Enabled()
AND
SysStatus("DOS2ModWrapper", _Status)
AND
_Status > 1
THEN
SysActivateGoal("LLENEMY_30_Origins_HardMode");
//END_REGION

//REGION DISABLING
PROC
LLENEMY_HardMode_Disabled()
AND
SysIsActive("LLENEMY_30_Origins_HardMode")
THEN
SysCompleteGoal("LLENEMY_30_Origins_HardMode");

PROC
LLENEMY_HardMode_DisableScripts()
AND
DB_LLENEMY_HardMode_Origins_Scripts(_Region, _Script)
AND
SysIsActive(_Script)
THEN
SysCompleteGoal(_Script);
//END_REGION

//REGION NPC_SETUP
PROC
LLENEMY_HardMode_SetupNPC((CHARACTERGUID)_Char, (STRING)_Region, (INTEGER)_Immortal)
THEN
//LeaderLib_Helper_MakeHostileToPlayers(_Char);
SetFaction(_Char, "Evil NPC");
SetVarInteger(_Char, "FleeFromDangerousSurface", 0);

PROC
LLENEMY_HardMode_SetupNPC((CHARACTERGUID)_Char, (STRING)_Region, 1)
AND
CharacterIsInCombat(_Char, 0)
THEN
ProcSetInvulnerable(_Char, 1);
CharacterSetImmortal(_Char, 1);
//END_REGION

//REGION TRACKING_SOURCE_USED
PROC
LLENEMY_HardMode_TrackTotalSourceUsed((INTEGER)_SourcePointsUsed)
AND
DB_CurrentLevel(_Region)
AND
NOT DB_LLENEMY_HardMode_SourcePointsUsed(_Region, _)
THEN
DB_LLENEMY_HardMode_SourcePointsUsed(_Region, 0);

PROC
LLENEMY_HardMode_TrackTotalSourceUsed((INTEGER)_SourcePointsUsed)
AND
DB_CurrentLevel(_Region)
AND
DB_LLENEMY_HardMode_SourcePointsUsed(_Region, _LastSP)
AND
IntegerSum(_LastSP, _SourcePointsUsed, _Next)
THEN
NOT DB_LLENEMY_HardMode_SourcePointsUsed(_Region, _LastSP);
DB_LLENEMY_HardMode_SourcePointsUsed(_Region, _Next);

IF
RegionEnded(_Region)
AND
DB_LLENEMY_HardMode_SourcePointsUsed(_Region, _LastSP)
THEN
NOT DB_LLENEMY_HardMode_SourcePointsUsed(_Region, _LastSP);

PROC
LeaderLib_Mods_OnVersionChanged(046aafd8-ba66-4b37-adfb-519c1a5d04d7, (INTEGER)_LastVersion, (INTEGER)_NewVersion)
AND
_LastVersion < 536870925
AND
DB_LLENEMY_HardMode_SourcePointsUsed(_Region, _TotalSP)
AND
_TotalSP < 0
THEN
NOT DB_LLENEMY_HardMode_SourcePointsUsed(_Region, _TotalSP);
DB_LLENEMY_HardMode_SourcePointsUsed(_Region, 0);
//END_REGION
EXITSECTION
ENDEXITSECTION
ParentTargetEdge "LaughingLeader_EnemyUpgradeOverhaul"