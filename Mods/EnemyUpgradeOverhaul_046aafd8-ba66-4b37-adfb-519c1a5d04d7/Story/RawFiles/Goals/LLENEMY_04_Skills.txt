Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLENEMY_SKills_InitSettings();

KBSECTION
//REGION SETTINGS
PROC
LLENEMY_SKills_InitSettings()
THEN
LLENEMY_Skills_AddChickenMember("LLENEMY_Animals_Chicken_BattleChicken_C_09ed8c09-a51c-4ee2-aa41-a5f4332ac861");
LLENEMY_Skills_AddChickenMember("LLENEMY_Animals_Chicken_BattleChicken_B_3e25cbea-a588-46ae-9020-2e081676966d");
LLENEMY_Skills_AddChickenMember("LLENEMY_Animals_Chicken_BattleChicken_A_2f26c1bf-5ecd-41f2-a692-9333d8435110");
//LLENEMY_Skills_AddChickenMember("LLENEMY_Animals_Chicken_BattleChicken_A_2f26c1bf-5ecd-41f2-a692-9333d8435110");
//LLENEMY_Skills_AddChickenMember("LLENEMY_Animals_Chicken_BattleChicken_A_2f26c1bf-5ecd-41f2-a692-9333d8435110");

PROC
LLENEMY_Skills_AddChickenMember((STRING)_Template)
AND
SysCount("DB_LLENEMY_ChickenArmada_Members", 2, _Count)
AND
IntegerSum(_Count, 1, _Index)
THEN
DB_LLENEMY_ChickenArmada_Members(_Index, _Template);
//END_REGION

//REGION RESET_DATA
PROC
LLENEMY_Skills_ResetData()
THEN
SysClear("DB_LLENEMY_ChickenArmada_Members", 2);
LLENEMY_SKills_InitSettings();
//END_REGION

//REGION UPDATES
PROC
LeaderUpdater_ModUpdated("EnemyUpgradeOverhaul", "LaughingLeader", (STRING)_OldVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_OldVersion, 1, 1, 1, 0)
THEN
LLENEMY_Skills_ResetData();
LeaderLog_Log("DEBUG", "[LLENEMY:Skills:LeaderUpdater_ModUpdated] Updating skill settings from version [",_OldVersion,"].");
//END_REGION

//REGION CHICKEN_ARMADA
IF
SkillCast(_Enemy, "Shout_LLENEMY_SummonChickenArmada", _, _)
AND
SysCount("DB_LLENEMY_ChickenArmada_Members", 2, _LastIndex)
THEN
LLENEMY_Skills_CreateChickenArmada(_Enemy, _LastIndex);
//ProcObjectTimer(_Enemy, "LLENEMY_Timers_SetChickenArmadaAttitude", 500);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Enemy, "LLENEMY_Timers_SetChickenArmadaAttitude")
AND
DB_LLENEMY_Temp_ChickenArmada(_Enemy, _Chicken)
AND
DB_LLENEMY_Temp_ChickenArmada(_Enemy, _Chicken2)
AND
_Chicken != _Chicken2
THEN
CharacterAddAttitudeTowardsPlayer(_Chicken, _Chicken2, 200);
CharacterAddAttitudeTowardsPlayer(_Chicken2, _Chicken, 200);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Enemy, "LLENEMY_Timers_SetChickenArmadaAttitude")
AND
DB_LLENEMY_Temp_ChickenArmada(_Enemy, _Chicken)
THEN
NOT DB_LLENEMY_Temp_ChickenArmada(_Enemy, _Chicken);

PROC
LLENEMY_Skills_CreateChickenArmada((CHARACTERGUID)_Enemy, (INTEGER)_Remaining)
AND
_Remaining > 0
AND
GetPosition(_Enemy, _x, _y, _z)
AND
FindValidPosition(_x, _y, _z, 20.0, _Enemy, _tx, _ty, _tz)
AND
DB_LLENEMY_ChickenArmada_Members(_Remaining, _Template)
AND
TemporaryCharacterCreateAtPosition(_tx, _ty, _tz, _Template, 1, _Chicken)
AND
IntegerSubtract(_Remaining, 1, _NextRemaining)
AND
GetFaction(_Enemy, _Faction)
THEN
DB_LLENEMY_Temp_ChickenArmada(_Enemy, _Chicken);
SetTag(_Chicken, "LLENEMY_IsTemporaryCharacter");
SetTag(_Chicken, "LLENEMY_ChickenArmada");
SetVarObject(_Chicken, "LLENEMY_ChickenOverlord", _Enemy);
PlayEffect(_Chicken, "RS3_FX_Skills_Totem_Impact_Summon_Overlay_01");
ApplyStatus(_Chicken, "SUMMONING_ABILITY", 180.0, 0, _Enemy);
SetFaction(_Chicken, _Faction);
//ProcSetRelationToPlayers(_Chicken, 0);
//CharacterAddAttitudeTowardsPlayer(_Chicken, _Enemy, 200);
CharacterSetCorpseLootable(_Chicken, 0);
LLENEMY_Skills_CreateChickenArmada(_Enemy, _NextRemaining);

IF
CharacterCharacterEvent(_Chicken, _Enemy, "LLENEMY_BattleChicken_FollowOwner")
THEN
ProcCharacterFollowCharacter(_Chicken, _Enemy);

IF
CharacterPrecogDying(_Chicken)
AND
IsTagged(_Chicken, "LLENEMY_ChickenArmada", 1)
AND
GetPosition(_Chicken, _x, _y, _z)
THEN
//CharacterFlushQueue(_Chicken);
PlayEffect(_Chicken, "RS3_FX_Skills_Totem_Impact_Cannibalize_01");
PlayEffectAtPosition("RS3_FX_Skills_Totem_Impact_Target_Touch_Root_01_Addon_Feathers", _x, _y, _z);
CharacterSetAnimationOverride(_Chicken, "die_front_down");

IF
StoryEvent((CHARACTERGUID)_Character, "LLENEMY_OnBattleChickenDied")
AND
DB_LLENEMY_Temp_ChickenArmada(_Enemy, _Chicken)
THEN
NOT DB_LLENEMY_Temp_ChickenArmada(_Enemy, _Chicken);
ProcObjectTimer(_Character, "LLENEMY_Timers_RemoveTemporaryCharacter", 1200);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Character, "LLENEMY_Timers_RemoveTemporaryCharacter")
AND
IsTagged(_Character, "LLENEMY_IsTemporaryCharacter", 1)
THEN
RemoveTemporaryCharacter(_Character);
//END_REGION

//REGION DRILLWORM
IF
CharacterUsedSkillAtPosition(_Worm, _X, _Y, _Z, "Jump_LLENEMY_EnemyDrillwormBurrow", _, _)
THEN
SetVarFloat3(_Worm, "SkillTargetPos_Jump_EnemyDrillwormBurrow", _X, _Y, _Z);
//END_REGION

//REGION DIG_GRAVE
IF
CharacterUsedSkillOnTarget(_Char, _Target, "Target_LLENEMY_DigGrave", _, _)
THEN
DB_LLENEMY_Skills_Temp_DigGraveTarget(_Char, _Target);

/* [OSITOOLS_ONLY]
IF
SkillCast(_Char, "Target_LLENEMY_DigGrave", _, _)
AND
GetUUID(_Char, _UUID)
THEN
NRD_ModCall("EnemyUpgradeOverhaul", "DigGrave_StartDigging", _UUID, _UUID2);

IF
StoryEvent((CHARACTERGUID)_Char, "LLENEMY_DigGrave_OnDiggingDone")
AND
DB_LLENEMY_Skills_Temp_DigGraveTarget(_Char, _Target)
AND
GetUUID(_Char, _UUID)
AND
GetUUID(_Target, _UUID2)
THEN
NOT DB_LLENEMY_Skills_Temp_DigGraveTarget(_Char, _Target);
NRD_ModCall("EnemyUpgradeOverhaul", "DigGrave_OnDiggingDone", _UUID, _UUID2);

IF
CharacterItemEvent(_Char, _DirtMound, "LLENEMY_DigGrave_DigUpPile")
AND
DB_LLENEMY_Skills_Temp_DigGraveMound(_Region, _OriginalBurialChar, _Target, _DirtMound)
AND
GetUUID(_Char, _UUID)
THEN
DB_LLENEMY_Skills_Temp_DigGraveTarget(_Char, (GUIDSTRING)_Target);
NRD_ModCall("EnemyUpgradeOverhaul", "DigGrave_StartDigging", _UUID);
*/

// [BEGIN_NO_OSITOOLS]
IF
StoryEvent((CHARACTERGUID)_Char, "LLENEMY_DigGrave_OnDiggingDone")
AND
DB_LLENEMY_Skills_Temp_DigGraveTarget(_Char, _Target)
THEN
NOT DB_LLENEMY_Skills_Temp_DigGraveTarget(_Char, _Target);
// [END_NO_OSITOOLS]

PROC
LLENEMY_Skills_DigGrave_SaveDirtMound((STRING)_Region, (CHARACTERGUID)_Char, (GUIDSTRING)_Target, (ITEMGUID)_DirtMound)
THEN
DB_LLENEMY_Skills_Temp_DigGraveMound(_Region, _Char, _Target, _DirtMound);

IF
RegionEnded(_Region)
AND
DB_LLENEMY_Skills_Temp_DigGraveMound(_Region, _Char, _Target, _DirtMound)
THEN
NOT DB_LLENEMY_Skills_Temp_DigGraveMound(_Region, _Char, _Target, _DirtMound);

IF
StoryEvent((CHARACTERGUID)_Target, "LLENEMY_DigGrave_ReleaseTarget")
AND
DB_LLENEMY_Skills_Temp_DigGraveMound(_Region, _Char, _Target, _DirtMound)
THEN
NOT DB_LLENEMY_Skills_Temp_DigGraveMound(_Region, _Char, _Target, _DirtMound);
ClearTag(_Target, "BLOCK_RESURRECTION");
ClearTag(_Target, "LLENEMY_BURIED");
SetVisible(_Target, 1);
ItemDestroy(_DirtMound);
//END_REGION

//REGION BURIAL_LOOT_GENERATION
/* [OSITOOLS_ONLY]
PROC
LLENEMY_Skills_DigGrave_OnBuried((CHARACTERGUID)_Char, (GUIDSTRING)_Target)
AND
ObjectGetFlag(_Target, "LLENEMY_DigGrave_BonusLootRolled", 0)
AND
GetUUID(_Char, _UUID)
AND
GetUUID(_Target, _UUID2)
THEN
ObjectSetFlag(_Target, "LLENEMY_DigGrave_BonusLootRolled", 0);
NRD_ModCall("EnemyUpgradeOverhaul", "DigGrave_RollForBonusLoot", _UUID, _UUID2);
*/

PROC
LLENEMY_Skills_DigGrave_OnBuried((CHARACTERGUID)_Char, (GUIDSTRING)_Target)
AND
ObjectIsCharacter((CHARACTERGUID)_Target, 1)
THEN
GenerateItems(_Char, _Target);

PROC
LLENEMY_Skills_DigGrave_OnBuried((CHARACTERGUID)_Char, (GUIDSTRING)_Target)
THEN
MoveAllItemsTo(_Target, _Char, 0, 0, 0);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_EnemyUpgradeOverhaul"