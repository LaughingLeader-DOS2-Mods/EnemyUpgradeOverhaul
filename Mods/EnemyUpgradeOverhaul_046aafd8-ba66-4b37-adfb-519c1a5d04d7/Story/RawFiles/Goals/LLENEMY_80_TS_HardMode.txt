Version 1
SubGoalCombiner SGC_AND
INITSECTION
KBSECTION
PROC
LLENEMY_Upgrades_OnRollingDone((CHARACTERGUID)_Char, (STRING)_Level)
AND
NOT DB_LLENEMY_HardMode_Temp_BonusRolls(_Char, _, _)
AND
LeaderLib_Random_QRY(1,3)
AND
DB_LeaderLib_Random(_BonusRolls)
THEN
NOT DB_LeaderLib_Random(_BonusRolls);
DB_LLENEMY_HardMode_Temp_BonusRolls(_Char, _BonusRolls, 0);

PROC
LLENEMY_Upgrades_OnRollingDone((CHARACTERGUID)_Char, (STRING)_Level)
AND
DB_LLENEMY_HardMode_Temp_BonusRolls(_Char, _BonusRolls, _Current)
AND
_Current < _BonusRolls
THEN
LeaderLib_Timers_StartObjectTimer(_Char, 250, "Timers_LLENEMY_Upgrades_RollBonusRolls", "LLENEMY_Upgrades_RollBonusRolls");
LLENEMY_UpgradeInfo_RestartTimers(300);

IF
StoryEvent((CHARACTERGUID)_Char, "LLENEMY_Upgrades_RollBonusRolls")
AND
DB_LLENEMY_HardMode_Temp_BonusRolls(_Char, _BonusRolls, _Current)
AND
_Current < _BonusRolls
AND
IntegerSum(_Current, 1, _Next)
AND
GetRegion(_Char, _Level)
THEN
NOT DB_LLENEMY_HardMode_Temp_BonusRolls(_Char, _BonusRolls, _Current);
DB_LLENEMY_HardMode_Temp_BonusRolls(_Char, _BonusRolls, _Next);
LLENEMY_Upgrades_RollForUpgrades(_Char);
LLENEMY_Upgrades_OnRollingDone(_Char, _Level);

//Cleanup
IF
RegionEnded(_)
THEN
SysClear("DB_LLENEMY_HardMode_Temp_BonusRolls", 3);

EXITSECTION
SysClear("DB_LLENEMY_HardMode_Temp_BonusRolls", 3);
ENDEXITSECTION
ParentTargetEdge "LLENEMY_80_ToggledScripts"