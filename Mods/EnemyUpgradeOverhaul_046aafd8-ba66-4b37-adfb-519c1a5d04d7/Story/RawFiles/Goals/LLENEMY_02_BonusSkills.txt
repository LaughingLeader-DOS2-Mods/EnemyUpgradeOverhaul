Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLENEMY_BonusSkills_InitSettings();
KBSECTION
//REGION SETTINGS
PROC
LLENEMY_Upgrades_ResetUpgradeData()
THEN
LLENEMY_BonusSkills_InitSettings();

PROC
LLENEMY_BonusSkills_InitSettings()
AND
LeaderLib_QRY_ExtenderIsActive()
THEN
LLENEMY_Upgrades_AddGroup("BonusSkills", "LLENEMY_BonusSkillsUpgradesDisabled", "LLENEMY_BonusSkillsRolled", "LLENEMY_BonusSkillsUpgradeAdded");

LLENEMY_Upgrades_AddType("BonusSkills", "Normal", 1, 600, 2);
LLENEMY_Upgrades_AddType("BonusSkills", "Elite", 800, 999, 4);
LLENEMY_Upgrades_AddType("BonusSkills", "Source1", 100, 150, 4);
LLENEMY_Upgrades_AddType("BonusSkills", "Source2", 850, 900, 4);

DB_LLENEMY_BonusSkills_Settings("LLENEMY_BONUSSKILLS_SINGLE", 1, 0);
DB_LLENEMY_BonusSkills_Settings("LLENEMY_BONUSSKILLS_SET_NORMAL", 3, 0);
DB_LLENEMY_BonusSkills_Settings("LLENEMY_BONUSSKILLS_SET_ELITE", 5, 2);
DB_LLENEMY_BonusSkills_Settings("LLENEMY_BONUSSKILLS_SOURCE", 1, 1);

PROC
LLENEMY_Upgrades_Register_TypeUpgrades("BonusSkills", "Normal")
THEN
LLENEMY_Upgrades_AddStatus("BonusSkills", "Normal", "LLENEMY_BONUSSKILLS_SINGLE", 700, 2);
LLENEMY_Upgrades_AddStatus("BonusSkills", "Normal", "LLENEMY_BONUSSKILLS_SET_NORMAL", 299, 6);

PROC
LLENEMY_Upgrades_Register_TypeUpgrades("BonusSkills", "Elite")
THEN
LLENEMY_Upgrades_AddStatus("BonusSkills", "Elite", "LLENEMY_BONUSSKILLS_SET_ELITE", 899, 10);

PROC
LLENEMY_Upgrades_Register_TypeUpgrades("BonusSkills", "Source1")
THEN
LLENEMY_Upgrades_AddStatus("BonusSkills", "Source1", "LLENEMY_BONUSSKILLS_SOURCE", 998, 4);

PROC
LLENEMY_Upgrades_Register_TypeUpgrades("BonusSkills", "Source2")
THEN
LLENEMY_Upgrades_AddStatus("BonusSkills", "Source2", "LLENEMY_BONUSSKILLS_SOURCE", 998, 4);

//END_REGION

//REGION ROLLING
// [BEGIN_NO_OSITOOLS]
PROC
LLENEMY_Upgrades_CanRollForGroupUpgrade((CHARACTERGUID)_Enemy, "BonusSkills", (STRING)_Type)
THEN
// Disabled without the extender
DB_LLENEMY_StatusUpgradeBlocked(_Enemy, "BonusSkills", _Type);
// [END_NO_OSITOOLS]

/* [OSITOOLS_ONLY]
PROC
LLENEMY_Upgrades_CanRollForGroupUpgrade((CHARACTERGUID)_Enemy, "BonusSkills", (STRING)_Type)
AND
NOT LeaderLib_Helper_QRY_CharacterIsHumanoid(_Enemy)
THEN
DB_LLENEMY_StatusUpgradeBlocked(_Enemy, "BonusSkills", _Type);
*/

PROC
LLENEMY_Upgrades_CanRollForGroupUpgrade((CHARACTERGUID)_Enemy, "BonusSkills", (STRING)_Type)
AND
NOT DB_LLENEMY_StatusUpgradeBlocked(_Enemy, "BonusSkills", _Type)
THEN
SetStoryEvent(_Enemy, "LeaderLog_Commands_SetSkillRequirement");

// Prevent Magisters from getting source skills
PROC
LLENEMY_Upgrades_CanRollForStatusUpgrade((CHARACTERGUID)_Enemy, (STRING)_Group, (STRING)_Type, "LLENEMY_BONUSSKILLS_SOURCE")
AND
IsTagged(_Enemy, "MAGISTER", 1)
AND
IsBoss(_Enemy, 0)
THEN
DB_LLENEMY_StatusUpgradeBlocked(_Enemy, _Group, _Type, "LLENEMY_BONUSSKILLS_SOURCE");

IF
DB_LLENEMY_StatusUpgradeBlocked(_Enemy, _Group, _Type, "LLENEMY_BONUSSKILLS_SOURCE")
AND
DB_LLENEMY_Upgrades_Statuses(_Group, _Type, "LLENEMY_BONUSSKILLS_SINGLE", _MinRoll, _MaxRoll, _Duration, _CP)
AND
HasActiveStatus(_Enemy, "LLENEMY_BONUSSKILLS_SINGLE", 0)
THEN
LLENEMY_Upgrades_ApplyUpgrade(_Enemy, _Group, _Type, "LLENEMY_BONUSSKILLS_SINGLE", _Duration, _CP);

/* [OSITOOLS_ONLY]
PROC
LLENEMY_Upgrades_ApplyUpgrade((CHARACTERGUID)_Enemy, "BonusSkills", (STRING)_Type, (STRING)_Status, (REAL)_Duration, (INTEGER)_CP)
AND
String(_Enemy, _EnemyStr)
AND
DB_LLENEMY_BonusSkills_Settings(_Status, _AddSkills, _SourceAllowed)
AND
IntegertoString(_AddSkills, _AddSkillsStr)
AND
IntegertoString(_SourceAllowed, _SourceAllowedStr)
THEN
NRD_LuaCall("LLENEMY_Ext_AddBonusSkills", _EnemyStr, _AddSkillsStr, _SourceAllowedStr);
*/

IF
CharacterStatusAttempt(_Char, "LLENEMY_BONUSSKILLS_SOURCE", _)
AND
CharacterGetMaxSourcePoints(_Char, _Max)
AND
IntegerSum(_Max, 1, _NextMax)
AND
IntegerMin(3, _NextMax, _Next) // Cap it at 3 max
THEN
CharacterOverrideMaxSourcePoints(_Char, _Next);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_EnemyUpgradeOverhaul"