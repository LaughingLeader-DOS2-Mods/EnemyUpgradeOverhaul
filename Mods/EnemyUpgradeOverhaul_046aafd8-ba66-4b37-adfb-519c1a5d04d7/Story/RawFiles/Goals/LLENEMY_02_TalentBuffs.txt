Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLENEMY_Talents_InitSettings();
KBSECTION
//REGION SETTINGS
PROC
LLENEMY_Upgrades_ResetUpgradeData()
THEN
LLENEMY_Talents_InitSettings();

PROC
LLENEMY_Talents_InitSettings()
THEN
LLENEMY_Upgrades_AddGroup("Talents", "LLENEMY_TalentUpgradesDisabled", "LLENEMY_TalentUpgradeRolled", "LLENEMY_TalentUpgradeAdded");
LLENEMY_Upgrades_AddType("Talents", "Elite", 1, 99, 8);
LLENEMY_Upgrades_AddType("Talents", "Normal", 360, 499, 5);
LLENEMY_Upgrades_AddType("Talents", "Weapon", 50, 600, 4);

PROC
LLENEMY_Upgrades_Register_TypeUpgrades("Talents", "Elite")
THEN
LLENEMY_Upgrades_AddStatus("Talents", "Elite", "LLENEMY_TALENT_LIGHTNINGROD", 260, 4);
LLENEMY_Upgrades_AddStatus("Talents", "Elite", "LLENEMY_TALENT_LONEWOLF", 100, 12);
LLENEMY_Upgrades_AddStatus("Talents", "Elite", "LLENEMY_TALENT_RESISTDEAD", 50, 8);
LLENEMY_Upgrades_AddStatus("Talents", "Elite", "LLENEMY_TALENT_RESISTDEAD2", 20, 10);
LLENEMY_Upgrades_AddStatus("Talents", "Elite", "LLENEMY_TALENT_TORTURER", 259, 4);
LLENEMY_Upgrades_AddStatus("Talents", "Elite", "LLENEMY_TALENT_UNSTABLE", 70, 5);
LLENEMY_Upgrades_AddStatus("Talents", "Elite", "LLENEMY_TALENT_WHATARUSH", 240, 6);
LLENEMY_Upgrades_AddStatus("Talents", "Elite", "LLENEMY_TALENT_BULLY", 120, 6);

PROC
LLENEMY_Upgrades_Register_TypeUpgrades("Talents", "Normal")
THEN
LLENEMY_Upgrades_AddStatus("Talents", "Normal", "LLENEMY_TALENT_LEECH", 399, 2);
LLENEMY_Upgrades_AddStatus("Talents", "Normal", "LLENEMY_TALENT_QUICKSTEP", 500, 4);
LLENEMY_Upgrades_AddStatus("Talents", "Normal", "LLENEMY_TALENT_NATURALCONDUCTOR", 100, 4);

PROC
LLENEMY_Upgrades_Register_TypeUpgrades("Talents", "Weapon")
THEN
LLENEMY_Upgrades_AddStatus("Talents", "Weapon", "LLENEMY_TALENT_RANGERRANGE", 500, 3);
//LLENEMY_Upgrades_AddStatus("Talents", "Weapon", "LLENEMY_TALENT_BACKSTAB", 499, 4);
//END_REGION

//REGION GIFTBAG_TALENTS
PROC
LLENEMY_Talents_InitSettings()
AND
SysIsActive("CMP_Talents")
THEN
LLENEMY_Talents_InitGiftBagTalents();

PROC
LLENEMY_Talents_InitGiftBagTalents()
AND
GlobalGetFlag("LLENEMY_TalentUpgrades_DivineTalentsAdded", 0)
THEN
LLENEMY_Upgrades_AddStatus("Talents", "Weapon", "LLENEMY_TALENT_SADIST", 50, 10);
LLENEMY_Upgrades_AddStatus("Talents", "Weapon", "LLENEMY_TALENT_GLADIATOR", 100, 6);
LLENEMY_Upgrades_AddStatus("Talents", "Elite", "LLENEMY_TALENT_HAYMAKER", 100, 8);
LLENEMY_Upgrades_AddStatus("Talents", "Elite", "LLENEMY_TALENT_INDOMITABLE", 50, 12);
LLENEMY_Upgrades_AddStatus("Talents", "Elite", "LLENEMY_TALENT_SOULCATCHER", 20, 12);
LLENEMY_Upgrades_AddStatus("Talents", "Elite", "LLENEMY_TALENT_MAGICCYCLES", 100, 6);
LLENEMY_Upgrades_AddStatus("Talents", "Normal", "LLENEMY_TALENT_MASTERTHIEF", 90, 4);
LLENEMY_Upgrades_AddStatus("Talents", "Normal", "LLENEMY_TALENT_GREEDYVESSEL", 90, 4);
GlobalSetFlag("LLENEMY_TalentUpgrades_DivineTalentsAdded");
//END_REGION

//REGION ALREADY_HAS_TALENT_BELT
// [BEGIN_NO_OSITOOLS]
PROC
LLENEMY_Upgrades_CanRollForGroupUpgrade((CHARACTERGUID)_Enemy, "Talents", (STRING)_Type)
AND
CharacterGetEquippedItem(_Enemy, "Belt", _Belt)
AND
_Belt != NULL_00000000-0000-0000-0000-000000000000
THEN
DB_LLENEMY_StatusUpgradeBlocked(_Enemy, "Talents", _Type);
// [END_NO_OSITOOLS]
//END_REGION

//REGION MELEE_WEAPON_TALENT
PROC
LLENEMY_Upgrades_CanRollForGroupUpgrade((CHARACTERGUID)_Enemy, "Talents", "Melee")
THEN
DB_LLENEMY_StatusUpgradeBlocked(_Enemy, "Talents", "Melee");
//END_REGION

//REGION WEAPON_TALENT_ROLLING
PROC
LLENEMY_Upgrades_CanRollForGroupUpgrade((CHARACTERGUID)_Enemy, "Talents", "Weapon")
THEN
DB_LLENEMY_StatusUpgradeBlocked(_Enemy, "Talents", "Weapon");

PROC
LLENEMY_Upgrades_RollForStatusUpgrade((CHARACTERGUID)_Enemy, "Talents", "Weapon")
AND
NOT DB_LLENEMY_Talents_Temp_WeaponTalentAdded(_Enemy)
AND
CharacterGetEquippedWeapon(_Enemy, (ITEMGUID)_Weapon)
AND
LeaderLib_Helper_QRY_IsNonMagicalRangedWeapon(_Weapon)
AND
LeaderLib_Random_QRY(999)
AND
DB_LeaderLib_Random(_Roll)
AND
LLENEMY_Upgrades_QRY_Internal_CheckForTypeCriticalFailure(_Enemy, "Talents", "Weapon", _Roll)
AND
DB_LLENEMY_Upgrades_Statuses(_Group, _Type, "LLENEMY_TALENT_RANGERRANGE", _MinRoll, _MaxRoll, _Duration, _ChallengePoints)
AND
_Roll <= _MaxRoll
AND
_Roll >= _MinRoll
THEN
NOT DB_LeaderLib_Random(_Roll);
LeaderLog_Log("TRACE", "[LLENEMY:Talents:RollForStatusUpgrade] Roll successful for weapon talent status [LLENEMY_TALENT_RANGERRANGE].");
LLENEMY_Upgrades_ApplyUpgrade(_Enemy, _Group, _Type, "LLENEMY_TALENT_RANGERRANGE", _Duration, _ChallengePoints);
DB_LLENEMY_Talents_Temp_WeaponTalentAdded(_Enemy);

//Backstab
/*
PROC
LLENEMY_Upgrades_RollForStatusUpgrade((CHARACTERGUID)_Enemy, "Talents", "Weapon")
AND
NOT DB_LLENEMY_Talents_Temp_WeaponTalentAdded(_Enemy)
AND
CharacterGetEquippedWeapon(_Enemy, (ITEMGUID)_Weapon)
AND
GetTemplate(_Weapon, _Template)
AND
StringContains(_Template, "_Dagger_", 0)
AND
LeaderLib_Random_QRY(999)
AND
DB_LeaderLib_Random(_Roll)
AND
LLENEMY_Upgrades_QRY_Internal_CheckForTypeCriticalFailure(_Enemy, "Talents", "Weapon", _Roll)
AND
DB_LLENEMY_Upgrades_Statuses(_Group, _Type, "LLENEMY_TALENT_BACKSTAB", _MinRoll, _MaxRoll, _Duration, _ChallengePoints)
AND
_Roll <= _MaxRoll
AND
_Roll >= _MinRoll
THEN
NOT DB_LeaderLib_Random(_Roll);
LeaderLog_Log("TRACE", "[LLENEMY:Talents:RollForStatusUpgrade] Roll successful for weapon talent status [LLENEMY_TALENT_BACKSTAB]. Applying to (",_Name,").");
LLENEMY_Upgrades_ApplyUpgrade(_Enemy, _Group, _Type, "LLENEMY_TALENT_BACKSTAB", _Duration, _ChallengePoints);
DB_LLENEMY_Talents_Temp_WeaponTalentAdded(_Enemy);
*/

//Gladiator
PROC
LLENEMY_Upgrades_RollForStatusUpgrade((CHARACTERGUID)_Enemy, "Talents", "Weapon")
AND
NOT DB_LLENEMY_Talents_Temp_WeaponTalentAdded(_Enemy)
AND
CharacterGetEquippedShield(_Enemy, _Shield)
AND
_Shield != NULL_00000000-0000-0000-0000-000000000000
AND
LeaderLib_Random_QRY(999)
AND
DB_LeaderLib_Random(_Roll)
AND
LLENEMY_Upgrades_QRY_Internal_CheckForTypeCriticalFailure(_Enemy, "Talents", "Weapon", _Roll)
AND
DB_LLENEMY_Upgrades_Statuses(_Group, _Type, "LLENEMY_TALENT_GLADIATOR", _MinRoll, _MaxRoll, _Duration, _ChallengePoints)
AND
_Roll <= _MaxRoll
AND
_Roll >= _MinRoll
THEN
NOT DB_LeaderLib_Random(_Roll);
LeaderLog_Log("TRACE", "[LLENEMY:Talents:RollForStatusUpgrade] Roll successful for weapon talent status [LLENEMY_TALENT_GLADIATOR].");
LLENEMY_Upgrades_ApplyUpgrade(_Enemy, _Group, _Type, "LLENEMY_TALENT_GLADIATOR", _Duration, _ChallengePoints);
DB_LLENEMY_Talents_Temp_WeaponTalentAdded(_Enemy);

//Sadist
PROC
LLENEMY_Upgrades_RollForStatusUpgrade((CHARACTERGUID)_Enemy, "Talents", "Weapon")
AND
NOT DB_LLENEMY_Talents_Temp_WeaponTalentAdded(_Enemy)
AND
CharacterGetEquippedShield(_Enemy, _Shield)
AND
_Shield != NULL_00000000-0000-0000-0000-000000000000
AND
LeaderLib_Random_QRY(999)
AND
DB_LeaderLib_Random(_Roll)
AND
LLENEMY_Upgrades_QRY_Internal_CheckForTypeCriticalFailure(_Enemy, "Talents", "Weapon", _Roll)
AND
DB_LLENEMY_Upgrades_Statuses(_Group, _Type, "LLENEMY_TALENT_SADIST", _MinRoll, _MaxRoll, _Duration, _ChallengePoints)
AND
_Roll <= _MaxRoll
AND
_Roll >= _MinRoll
THEN
NOT DB_LeaderLib_Random(_Roll);
LeaderLog_Log("TRACE", "[LLENEMY:Talents:RollForStatusUpgrade] Roll successful for weapon talent status [LLENEMY_TALENT_SADIST].");
LLENEMY_Upgrades_ApplyUpgrade(_Enemy, _Group, _Type, "LLENEMY_TALENT_SADIST", _Duration, _ChallengePoints);
DB_LLENEMY_Talents_Temp_WeaponTalentAdded(_Enemy);

PROC
LLENEMY_Upgrades_RollForStatusUpgrade((CHARACTERGUID)_Enemy, "Talents", "Weapon")
AND
DB_LLENEMY_Talents_Temp_WeaponTalentAdded(_Enemy)
THEN
NOT DB_LLENEMY_Talents_Temp_WeaponTalentAdded(_Enemy);
//END_REGION

//REGION LEVELING_TALENT_BELTS
PROC
LeaderUpdater_ModUpdated("EnemyUpgradeOverhaul", "LaughingLeader", (STRING)_OldVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_OldVersion, 1,3,0,0)
THEN
SysClear("DB_LLENEMY_Talents_Temp_DeleteItem", 2);


/* // No longer needed since the belt stats now start with an underscore, which let them auto-level and make them hidden from the UI
IF
CharacterStatusApplied(_Enemy, _Status, _)
AND
DB_LLENEMY_Upgrades_Statuses("Talents", _Type, _Status, _MinRoll, _MaxRoll, _Duration, _ChallengePoints)
THEN
LLENEMY_Talents_LevelUpTalentItems(_Enemy);

PROC
LLENEMY_Talents_LevelUpTalentItems((CHARACTERGUID)_Enemy)
AND
CharacterGetEquippedItem(_Enemy, "Belt", (ITEMGUID)_Belt)
AND
CharacterGetLevel(_Enemy, _Level)
THEN
ItemLevelUpTo(_Belt, _Level);
DB_LLENEMY_Talents_Temp_DeleteItem(_Enemy, _Belt);

PROC
LLENEMY_Talents_LevelUpTalentItems((CHARACTERGUID)_Enemy)
AND
CharacterGetEquippedItem(_Enemy, "Ring", (ITEMGUID)_Ring)
AND
CharacterGetLevel(_Enemy, _Level)
THEN
ItemLevelUpTo(_Ring, _Level);
DB_LLENEMY_Talents_Temp_DeleteItem(_Enemy, _Ring);

PROC
LLENEMY_Talents_LevelUpTalentItems((CHARACTERGUID)_Enemy)
AND
CharacterGetEquippedItem(_Enemy, "Ring2", (ITEMGUID)_Ring)
AND
CharacterGetLevel(_Enemy, _Level)
THEN
ItemLevelUpTo(_Ring, _Level);
DB_LLENEMY_Talents_Temp_DeleteItem(_Enemy, _Ring);

IF
CharacterDied(_Enemy)
AND
DB_LLENEMY_Talents_Temp_DeleteItem(_Enemy, _Item)
AND
ObjectExists(_Item, 1)
THEN
ItemRemove(_Item);

IF
CharacterDied(_Enemy)
AND
DB_LLENEMY_Talents_Temp_DeleteItem(_Enemy, _Item)
THEN
NOT DB_LLENEMY_Talents_Temp_DeleteItem(_Enemy, _Item);
*/
//END_REGION

//REGION NATURAL_CONDUCTOR
IF
CharacterStatusRemoved(_Char, "LLENEMY_TALENT_NATURALCONDUCTOR", _)
THEN
LeaderLib_ToggleScripts_DisableScriptForObject(_Char, "LLENEMY_NaturalConductor_Active", "EnemyUpgradeOverhaul");

IF
CharacterStatusApplied(_Char, "LLENEMY_TALENT_NATURALCONDUCTOR", _)
THEN
LeaderLib_ToggleScripts_EnableScriptForObject(_Char, "LLENEMY_NaturalConductor_Active", "EnemyUpgradeOverhaul");

QRY
LLENEMY_QRY_IsInElectrifiedSurface((CHARACTERGUID)_Character)
AND
GetSurfaceGroundAt(_Character, _Surface)
AND
StringContains(_Surface, "Electrified", 1)
THEN
DB_NOOP(1);

QRY
LLENEMY_QRY_IsInElectrifiedSurface((CHARACTERGUID)_Character)
AND
GetSurfaceCloudAt(_Character, _Surface)
AND
StringContains(_Surface, "Electrified", 1)
THEN
DB_NOOP(1);
//END_REGION

//REGION BULLY
QRY
LLENEMY_Talents_QRY_BullyTarget((GUIDSTRING)_Target)
AND
HasActiveStatus(_Target, "KNOCKED_DOWN", 1)
THEN
DB_NOOP(1);

QRY
LLENEMY_Talents_QRY_BullyTarget((GUIDSTRING)_Target)
AND
HasActiveStatus(_Target, "CRIPPLED", 1)
THEN
DB_NOOP(1);

QRY
LLENEMY_Talents_QRY_BullyTarget((GUIDSTRING)_Target)
AND
HasActiveStatus(_Target, "SLOWED", 1)
THEN
DB_NOOP(1);

IF
StoryEvent((CHARACTERGUID)_Enemy, "LLENEMY_Bully_RemoveBonus")
THEN
RemoveStatus(_Enemy, "LLENEMY_TALENT_BULLY_DAMAGEBONUS");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_EnemyUpgradeOverhaul"