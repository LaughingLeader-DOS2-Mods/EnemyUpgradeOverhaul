Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLENEMY_Duplication_InitSettings();
KBSECTION
//REGION SETTINGS
PROC
LLENEMY_Duplication_InitSettings()
THEN
LLENEMY_Upgrades_AddGroup("Duplication", "LLENEMY_DuplicationUpgradesDisabled", "LLENEMY_DuplicationRolled", "LLENEMY_DuplicationUpgradeAdded");

LLENEMY_Upgrades_AddType("Duplication", "Elite", 1, 99, 8);
LLENEMY_Upgrades_AddType("Duplication", "Normal", 550, 798, 5);

PROC
LLENEMY_Upgrades_Register_TypeUpgrades("Duplication", "Elite")
THEN
LLENEMY_Upgrades_AddStatus("Duplication", "Elite", "LLENEMY_DUPE_ELITE_SINGLE", 900, 11);
LLENEMY_Upgrades_AddStatus("Duplication", "Elite", "LLENEMY_DUPE_ELITE_DOUBLE", 99, 11);

PROC
LLENEMY_Upgrades_Register_TypeUpgrades("Duplication", "Normal")
THEN
LLENEMY_Upgrades_AddStatus("Duplication", "Normal", "LLENEMY_DUPE_NORMAL_WEAKEST", 500, 2);
LLENEMY_Upgrades_AddStatus("Duplication", "Normal", "LLENEMY_DUPE_NORMAL_WEAK", 300, 4);
LLENEMY_Upgrades_AddStatus("Duplication", "Normal", "LLENEMY_DUPE_NORMAL_SAME", 199, 6);
//END_REGION

//REGION Duplication
QRY
LLENEMY_Duplication_QRY_CanDuplicate((CHARACTERGUID)_Char, 1)
AND
GlobalGetFlag("LLENEMY_DuplicationUpgrades_BossesDisabled", 0)
THEN
DB_NOOP(1);

QRY
LLENEMY_Duplication_QRY_CanDuplicate((CHARACTERGUID)_Char, 0)
THEN
DB_NOOP(1);

PROC
LLENEMY_Upgrades_CanRollForGroupUpgrade((CHARACTERGUID)_Enemy, "Duplication", (STRING)_Type)
AND
IsBoss(_Enemy, _IsBoss)
AND
LLENEMY_Duplication_QRY_CanDuplicate(_Enemy, _IsBoss)
AND
DB_LLENEMY_Duplication_Temp_Active(_Enemy, _Dupe, _Region)
THEN
DB_LLENEMY_StatusUpgradeBlocked(_Enemy, "Duplication", _Type);

PROC
LLENEMY_Duplication_CreateDuplicate((CHARACTERGUID)_Source, (STRING)_Stats)
THEN
LLENEMY_Duplication_CreateDuplicate(_Source, _Stats, 0, 0, 0);

// [BEGIN_NO_OSITOOLS]
PROC
LLENEMY_Duplication_CreateDuplicate((CHARACTERGUID)_Source, (STRING)_Stats, (INTEGER)_CopyAttributes, (INTEGER)_CopyAbilities, (INTEGER)_ChallengePoints)
AND
DB_CurrentLevel(_Region)
AND
CharacterCreateOutOfSightToObject("LLENEMY_Dupe_A_54ad4e06-b57f-46d0-90fc-5da1208250e0", _Source, _Source, 0, "", _Dupe)
AND
GetFaction(_Source, _Faction)
THEN
CharacterTransformFromCharacter(_Dupe, _Source, 0, 1, 0, 1, 1, 0, 0);
CharacterTransformAppearanceTo(_Dupe, _Source, 0, 1);
SetFaction(_Dupe, _Faction);
ChangeAttitude(_Source, _Dupe, 200);
ChangeAttitude(_Dupe, _Source, 200);
SetVarFixedString(_Dupe, "LLENEMY_Dupe_Stats", _Stats);
SetVarInteger(_Dupe, "LLENEMY_ChallengePoints", _ChallengePoints);
DB_LLENEMY_Duplication_Temp_Active(_Source, _Dupe, _Region);
DB_LLENEMY_Duplication_Temp_CopySettings(_Source, _Dupe, _CopyAttributes, _CopyAbilities);
CharacterCharacterSetEvent(_Source, _Dupe, "LLENEMY_Dupe_ApplyStats");
// [END_NO_OSITOOLS]

/* [OSITOOLS_ONLY]
PROC
LLENEMY_Duplication_CreateDuplicate((CHARACTERGUID)_Source, (STRING)_Stats, (INTEGER)_CopyAttributes, (INTEGER)_CopyAbilities, (INTEGER)_ChallengePoints)
AND
DB_CurrentLevel(_Region)
AND
CharacterCreateOutOfSightToObject("LLENEMY_Dupe_A_54ad4e06-b57f-46d0-90fc-5da1208250e0", _Source, _Source, 0, "", _Dupe)
AND
GetFaction(_Source, _Faction)
AND
String(_Dupe, _DupeStr)
THEN
CharacterTransformFromCharacter(_Dupe, _Source, 0, 1, 1, 1, 1, 0, 0);
CharacterTransformAppearanceTo(_Dupe, _Source, 0, 1);
SetFaction(_Dupe, _Faction);
ChangeAttitude(_Source, _Dupe, 200);
ChangeAttitude(_Dupe, _Source, 200);
SetVarFixedString(_Dupe, "LLENEMY_Dupe_Stats", _Stats);
SetVarInteger(_Dupe, "LLENEMY_ChallengePoints", _ChallengePoints);
DB_LLENEMY_Duplication_Temp_Active(_Source, _Dupe, _Region);
DB_LLENEMY_Duplication_Temp_CopySettings(_Source, _Dupe, _CopyAttributes, _CopyAbilities);
CharacterCharacterSetEvent(_Source, _Dupe, "LLENEMY_Dupe_ApplyStats");
NRD_LuaCall("LLENEMY_Ext_ClearGain", _DupeStr);
*/

IF
CharacterCharacterEvent(_Source, _Dupe, "LLENEMY_Dupe_StatsApplied")
AND
DB_LLENEMY_Duplication_Temp_CopySettings(_Source, _Dupe, _CopyAttributes, _CopyAbilities)
THEN
NOT DB_LLENEMY_Duplication_Temp_CopySettings(_Source, _Dupe, _CopyAttributes, _CopyAbilities);
LLENEMY_Duplication_SetupDuplicate(_Source, _Dupe, _CopyAttributes, _CopyAbilities);

IF
CharacterCharacterEvent(_Source, _Dupe, "LLENEMY_Dupe_StatsApplied")
THEN
TeleportTo(_Dupe, _Source, "LLENEMY_DupeCharacterTeleported", 1, 1, 0);
CharacterSetFollowCharacter(_Dupe, _Source);
PlayEffect(_Dupe, "RS3_FX_GP_ScriptedEvent_Teleport_GenericSmoke_01");

IF
CharacterCharacterEvent(_Source, _Dupe, "LLENEMY_Dupe_StatsApplied")
AND
CharacterGetDisplayName(_Source, _, _Name)
AND
StringConcatenate(_Name, " (Shadow)", _DupeName)
THEN
CharacterSetCustomName(_Dupe, _DupeName);

PROC
LLENEMY_Duplication_SetupDuplicate((CHARACTERGUID)_Source, (CHARACTERGUID)_Dupe, 1, (INTEGER)_CopyAbilities)
THEN
LeaderLib_Helper_CopyAttributes(_Dupe, _Source);

PROC
LLENEMY_Duplication_SetupDuplicate((CHARACTERGUID)_Source, (CHARACTERGUID)_Dupe, (INTEGER)_CopyAttributes, 1)
THEN
LeaderLib_Helper_CopyAbilities(_Dupe, _Source);

PROC
LLENEMY_Duplication_SetupDuplicate((CHARACTERGUID)_Source, (CHARACTERGUID)_Dupe, (INTEGER)_CopyAttributes, (INTEGER)_CopyAbilities)
THEN
LeaderLib_Helper_CopyLevel(_Dupe, _Source);
//END_REGION

//REGION DUPE_STATUS_APPLIED
IF
CharacterStatusApplied(_Char, "LLENEMY_DUPE_ELITE_SINGLE", _)
AND
DB_LLENEMY_Upgrades_Statuses(_Group, _Type, "LLENEMY_DUPE_ELITE_SINGLE", _StartInt, _EndInt, _Duration, _ChallengePoints)
THEN
LLENEMY_Duplication_CreateDuplicate(_Char, "LLENEMY_Dupe_Elite_A", 1, 1, _ChallengePoints);

IF
CharacterStatusApplied(_Char, "LLENEMY_DUPE_ELITE_DOUBLE", _)
AND
DB_LLENEMY_Upgrades_Statuses(_Group, _Type, "LLENEMY_DUPE_ELITE_DOUBLE", _StartInt, _EndInt, _Duration, _ChallengePoints)
THEN
LLENEMY_Duplication_CreateDuplicate(_Char, "LLENEMY_Dupe_Elite_A", 1, 1, _ChallengePoints);
LLENEMY_Duplication_CreateDuplicate(_Char, "LLENEMY_Dupe_Elite_A", 1, 1, _ChallengePoints);

IF
CharacterStatusApplied(_Char, "LLENEMY_DUPE_NORMAL_SAME", _)
AND
DB_LLENEMY_Upgrades_Statuses(_Group, _Type, "LLENEMY_DUPE_NORMAL_SAME", _StartInt, _EndInt, _Duration, _ChallengePoints)
THEN
LLENEMY_Duplication_CreateDuplicate(_Char, "LLENEMY_Dupe_Normal_A", 1, 1, _ChallengePoints);

IF
CharacterStatusApplied(_Char, "LLENEMY_DUPE_NORMAL_WEAK", _)
AND
DB_LLENEMY_Upgrades_Statuses(_Group, _Type, "LLENEMY_DUPE_NORMAL_WEAK", _StartInt, _EndInt, _Duration, _ChallengePoints)
THEN
LLENEMY_Duplication_CreateDuplicate(_Char, "LLENEMY_Dupe_Normal_Weak", 0, 1, _ChallengePoints);

IF
CharacterStatusApplied(_Char, "LLENEMY_DUPE_NORMAL_WEAKEST", _)
AND
DB_LLENEMY_Upgrades_Statuses(_Group, _Type, "LLENEMY_DUPE_NORMAL_WEAKEST", _StartInt, _EndInt, _Duration, _ChallengePoints)
THEN
LLENEMY_Duplication_CreateDuplicate(_Char, "LLENEMY_Dupe_Normal_Weakest", 0, 1, _ChallengePoints);
//END_REGION

//REGION CLEANUP
IF
StoryEvent((CHARACTERGUID)_Dupe, "LLENEMY_Dupe_OnDupeDying")
AND
DB_LLENEMY_Duplication_Temp_Active(_Enemy, _Dupe, _Region)
THEN
NOT DB_LLENEMY_Duplication_Temp_Active(_Enemy, _Dupe, _Region);

IF
RegionEnded(_Region)
AND
DB_LLENEMY_Duplication_Temp_Active(_Enemy, _Dupe, _Region)
THEN
NOT DB_LLENEMY_Duplication_Temp_Active(_Enemy, _Dupe, _Region);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_EnemyUpgradeOverhaul"