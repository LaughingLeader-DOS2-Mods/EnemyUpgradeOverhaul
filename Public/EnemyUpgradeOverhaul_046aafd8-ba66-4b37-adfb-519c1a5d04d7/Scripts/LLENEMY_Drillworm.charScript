INIT
	CHARACTER:__Me
	LIST<INT64>:%effectHandles
	EXTERN FLOAT3:%appearPos=null
	
EVENTS

EVENT onShutdown_Clear
VARS
	INT:_Count
	INT:_Size
	INT64:_Handle
ON
	OnShutdown()
ACTIONS
	Set(_Count,1)
	IF "c1"
		ListGetSize(%effectHandles,_Size)
	THEN
		WHILE "!c1"
			IsGreaterThen(_Count,_Size)
		DO
			IF "c1"
				ListGet(%effectHandles,_Count,_Handle)
			THEN
				StopLoopEffect(_Handle)
			ENDIF
			Add(_Count,1)
		ENDWHILE
	ENDIF
	ListClear(%effectHandles)

EVENT onEvent_SetEffect
VARS
	INT:_Size
	FLOAT3:_Pos
	INT64:_Handle
	INT64:_HandleToKill
ON
	OnSkillCast(__Me,Jump_EnemyDrillwormBurrow)
	OnFunction("SetEffect")
ACTIONS
	IF "c1"
		GetVar(_Pos,__Me,"SkillTargetPos_Jump_EnemyDrillwormBurrow")
	THEN
	ELSE
		Set(_Pos,%appearPos)
	ENDIF
	
	IF "c1&!c2"
		ListGetSize(%effectHandles,_Size)
		IsEqual(_Pos,null)
	THEN
		IF "!c1"
			IsGreaterThen(_Size,2)
		THEN
			PlayLoopEffectAt(_Handle,_Pos,"RS3_FX_Char_Creatures_VoidWoken_Drillworm_A_Hole_Root_01")
			ListAdd(%effectHandles,_Handle)
		ELSE
			IF "c1"
				ListGet(%effectHandles,1,_HandleToKill)
			THEN
				StopLoopEffect(_HandleToKill)
				PlayLoopEffectAt(_Handle,_Pos,"RS3_FX_Char_Creatures_VoidWoken_Drillworm_A_Hole_Root_01")
				ListRemove(%effectHandles,1)
				ListAdd(%effectHandles,_Handle)
			ENDIF
		ENDIF
	ENDIF

BEHAVIOUR
REACTION inCombat_burrowToCenter,7
USAGE COMBAT
VARS
	CHARACTER:_Enemy
	FLOAT3:_pos
	FLOAT3:_myPos
	FLOAT:_maxRange
	FLOAT:_dist
CHECK "c1&(c2&c3&!c4)&c5&c6"
	CharacterGetEnemy(_Enemy,__Me)
	CharacterGetSkillRange(_,_maxRange,__Me,Projectile_LLENEMY_EnemyDrillwormSpit)
	GetDistance(_dist,__Me,_Enemy)
	IsGreaterThen(_dist,_maxRange)
	FindValidPosition(_pos,5,__Me)
	CharacterCanCast(__Me,Jump_EnemyDrillwormBurrow)
ACTIONS
	CharacterUseSkill(Jump_EnemyDrillwormBurrow,_pos)
	EndTurn(__Me)
INTERRUPT
ON
	OnBetterReactionFound(_)
ACTIONS
	Reset()

REACTION inPeace_burrowToCenter,10
USAGE PEACE
VARS
	CHARACTER:_Enemy
	FLOAT3:_pos
	FLOAT3:_myPos
	FLOAT:_maxRange
	FLOAT:_dist
CHECK "c1&c2&c3"
	GetDistance(_dist,__Me,%appearPos)
	IsGreaterThen(_dist,_maxRange)
	FindValidPosition(_pos,5,__Me)
ACTIONS
	CharacterUseSkill(Jump_EnemyDrillwormBurrow,_pos)